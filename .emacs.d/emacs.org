#+TITLE: My Emacs Configuration
#+AUTHOR: Alex Griffin
#+STARTUP: content
#+PROPERTY: header-args :tangle ~/.emacs.d/emacs.el

* General

This file describes my GNU Emacs configuration in a literate
programming style. Run the Emacs command =org-babel-tangle= on this
document to generate the actual config.

** Initialization

*** Optimize startup time

From doom-emacs and John Wiegley.

#+BEGIN_SRC emacs-lisp
  ;;; -*- lexical-binding: t -*-

  (defvar file-name-handler-alist-old file-name-handler-alist)

  ;; Only run this code once, even when reloading the file.
  (defvar pre-init-p t)
  (when pre-init-p
    (setq file-name-handler-alist nil
          pre-init-p nil
          gc-cons-threshold 402653184
          gc-cons-percentage 0.6
          load-prefer-newer t
          package-enable-at-startup nil
          package--init-file-ensured t))

  (add-hook 'after-init-hook
            `(lambda ()
               (setq file-name-handler-alist file-name-handler-alist-old
                     gc-cons-threshold 800000
                     gc-cons-percentage 0.1)
               (garbage-collect))
            t)
#+END_SRC

*** Persistence Files

Use a separate file for customization system.

#+BEGIN_SRC emacs-lisp
  (setq custom-file (concat user-emacs-directory "custom.el"))
  (load custom-file t)
#+END_SRC

Don't litter the filesystem with backup or auto-save files.

#+BEGIN_SRC emacs-lisp
  (setq make-backup-files nil)
  (setq auto-save-file-name-transforms
        `((".*" ,(concat user-emacs-directory "auto-save/") t)))
#+END_SRC

Save minibuffer history.

#+BEGIN_SRC emacs-lisp
  (savehist-mode 1)
#+END_SRC

*** Package Management

**** Specify Package Repositories

Use TLS for ELPA, and add MELPA as an additional repository.

#+BEGIN_SRC emacs-lisp
  (setq package-archives
        '(("gnu" . "https://elpa.gnu.org/packages/")
          ("melpa" . "https://melpa.org/packages/")
          ("melpa-stable" . "https://stable.melpa.org/packages/")))
#+END_SRC

**** Fix "Failed to download 'gnu' archive."

See https://debbugs.gnu.org/34341

#+BEGIN_SRC emacs-lisp
  (unless (version<= emacs-version "26.3")
    (setq gnutls-algorithm-priority "NORMAL:-VERS-TLS1.3"))
#+END_SRC

**** Bootstrap use-package

Every other package will be managed by use-package.

#+BEGIN_SRC emacs-lisp
  (eval-when-compile
    (require 'package)
    (package-initialize)
    (unless (locate-library "use-package")
      (package-refresh-contents)
      (package-install 'use-package))
    (require 'use-package))
  (require 'bind-key)
#+END_SRC

Don't load a package if it's not installed.

#+BEGIN_SRC emacs-lisp
  (add-to-list 'use-package-defaults
               '(:when
                 (lambda (package &rest _)
                   `(locate-library (symbol-name ',package)))
                 t))
#+END_SRC

Don't ensure packages that already installed through another package manager.

#+BEGIN_SRC emacs-lisp
  (defun my-use-package-ensure (name args state &optional no-refresh)
    (unless (locate-library (symbol-name name))
      (use-package-ensure-elpa name args state no-refresh)))
  (setq use-package-ensure-function 'my-use-package-ensure)
#+END_SRC

** Interface Elements

Emacs' interface is a little too busy for me by default.

Disable the tool bar and menu bar.

#+BEGIN_SRC emacs-lisp
  (if (fboundp 'menu-bar-mode) (menu-bar-mode -1))
  (if (fboundp 'tool-bar-mode) (tool-bar-mode -1))
#+END_SRC

Don't show the normal Emacs welcome screen or related messages.

#+BEGIN_SRC emacs-lisp
  (setq inhibit-startup-message t)
  (defun display-startup-echo-area-message ()
    (message ""))
#+END_SRC

Empty the scratch buffer and remove lisp-interaction-mode from it.

#+BEGIN_SRC emacs-lisp
  (setq initial-scratch-message nil
        initial-major-mode 'fundamental-mode)
#+END_SRC

Set the window title format.

#+BEGIN_SRC emacs-lisp
  (setq frame-title-format "%b - GNU Emacs")
  (setq icon-title-format frame-title-format)
#+END_SRC

Disambiguate buffer names by adding its file path to the name when needed.

#+BEGIN_SRC emacs-lisp
  (set uniquify-buffer-name-style 'forward)
#+END_SRC

Set default window geometry.

#+BEGIN_SRC emacs-lisp
  (setq default-frame-alist '((width . 80)
                              (height . 43)))
#+END_SRC

Scroll one line at a time.

#+BEGIN_SRC emacs-lisp
  (setq scroll-conservatively 10000)
#+END_SRC

Prompt for 'y' or 'n' instead of 'yes' or 'no'.

#+BEGIN_SRC emacs-lisp
  (fset 'yes-or-no-p 'y-or-n-p)
#+END_SRC

Save cursor position to resume editing files.

#+BEGIN_SRC emacs-lisp
  (setq save-place-file (concat user-emacs-directory "places"))
  (save-place-mode 1)
#+END_SRC

Disable that infernal beep!

#+BEGIN_SRC emacs-lisp
  (setq ring-bell-function 'ignore
        visible-bell nil)
#+END_SRC

Enable mouse support in terminal mode.

#+BEGIN_SRC emacs-lisp
  (xterm-mouse-mode 1)
#+END_SRC

Some miscellaneous settings from
[[https://github.com/technomancy/better-defaults][better-defaults]].

#+BEGIN_SRC emacs-lisp
  (setq save-interprogram-paste-before-kill t
        apropos-do-all t
        mouse-yank-at-point t
        require-final-newline t
        ediff-window-setup-function 'ediff-setup-windows-plain)
#+END_SRC

** Appearance

*** Theme

Use parchment, my own light theme inspired by Acme and Leuven.

#+BEGIN_SRC emacs-lisp
  (use-package parchment-theme
    :ensure t
    :load-path "~/src/parchment"
    :config
    (setq custom-safe-themes t
          parchment-add-mode-hooks t
          parchment-want-modify-tty-colors t)
    (load-theme 'parchment t))
#+END_SRC

Highlight matching braces & parentheses.

#+BEGIN_SRC emacs-lisp
  (show-paren-mode)
#+END_SRC

**** Solaire Mode

Give a brighter background to real files compared to other buffers.

#+BEGIN_SRC emacs-lisp
  (use-package solaire-mode
    :hook
    ((change-major-mode after-revert ediff-prepare-buffer) . turn-on-solaire-mode)
    (minibuffer-setup . solaire-mode-in-minibuffer)
    :config
    (solaire-global-mode +1)
    (solaire-mode-swap-bg)
    ;; Automatically swap solaire backgrounds when loading themes.
    (defun my-solaire-mode-swap-bg (&rest _rest)
      (solaire-mode-swap-bg))
    (advice-add 'load-theme :after #'my-solaire-mode-swap-bg))
#+END_SRC

*** Fonts

Set fonts, both now and when creating new frames (for ~emacsclient~).

#+BEGIN_SRC emacs-lisp
  (defun my-set-fonts (&optional frame)
    (set-face-font 'default           "Go Mono 11"   frame)
    (set-face-font 'fixed-pitch       "Noto Mono 11" frame)
    (set-face-font 'fixed-pitch-serif "Go Mono 11"   frame)
    (set-face-font 'variable-pitch    "Noto Sans"    frame))
  (my-set-fonts)
  (add-hook 'after-make-frame-functions 'my-set-fonts)
#+END_SRC

*** Cursor

Highlight the line that the cursor is currently on.

#+BEGIN_SRC emacs-lisp
  (global-hl-line-mode)
#+END_SRC

Fix describe-face when using hl-line-mode. From
https://emacs.stackexchange.com/a/45719:

#+BEGIN_SRC emacs-lisp
  (defun my-face-at-point ()
    (symbol-name
     (or (let ((face (get-text-property (point) 'face)))
           (or (and (face-list-p face)
                    (car face))
               (and (symbolp face)
                    face)))
         'default)))

  (eval-after-load "hl-line"
    '(progn
       (advice-add 'counsel--face-at-point :override #'my-face-at-point)))
#+END_SRC

*** Mode Line

I abuse some implementation details of =smart-mode-line= to put the
cursor position information on the right like vim.

#+BEGIN_SRC emacs-lisp
  (use-package smart-mode-line
    :ensure t
    :config
    (setq sml/mode-width 'right
          sml/pre-modes-separator "  "
          sml/theme nil)
    (add-to-list 'sml/replacer-regexp-list
                 `(,(concat "^/vcsh:dotfiles:" (getenv "HOME")) ":Dot:~") t)
    ;; Override this function to get better spacing once we rearrange.
    (defun sml/fill-for-buffer-identification () "  ")
    (column-number-mode) ;; Show column number next to the line number.
    (sml/setup)
    ;; Rearrange mode-line to put position and line number on the right.
    (setq-default
     mode-line-format
     '("%e"
       mode-line-mule-info
       mode-line-client
       mode-line-modified
       mode-line-remote
       "  "
       mode-line-frame-identification
       mode-line-buffer-identification
       sml/pos-id-separator
       (vc-mode vc-mode)
       sml/pre-modes-separator
       mode-line-modes
       mode-line-misc-info
       mode-line-front-space
       mode-line-position
       mode-line-end-spaces)))
#+END_SRC

**** Hide Mode Lighters

Most of my =diminish= invocations are within =use-package=
declarations, but some modes are hidden the hard way.

#+BEGIN_SRC emacs-lisp
  (use-package diminish
    :ensure t
    :config
    (eval-after-load "eldoc"
      '(diminish 'eldoc-mode)))
#+END_SRC

* Editing

** Whitespace

#+BEGIN_SRC emacs-lisp
  (use-package whitespace
    :diminish (whitespace-mode global-whitespace-mode)
    :config
    (setq whitespace-line-column 79
          whitespace-style '(face lines-tail trailing))
    (global-whitespace-mode 1))
#+END_SRC

Don't indent with tabs by default.

#+BEGIN_SRC emacs-lisp
  (setq-default indent-tabs-mode nil)
#+END_SRC

Sentences end with a single space.

#+BEGIN_SRC emacs-lisp
  (setq sentence-end-double-space nil)
#+END_SRC

Load style settings from =.editorconfig=

#+BEGIN_SRC emacs-lisp
  (use-package editorconfig
    :ensure t
    :diminish
    :hook (prog-mode . editorconfig-mode)
    :commands editorconfig-mode)
  #+END_SRC

Automatically trim whitespace only from lines edited.

#+BEGIN_SRC emacs-lisp
  (use-package ws-butler
    :ensure t
    :diminish
    :hook (prog-mode . ws-butler-mode)
    :commands ws-butler-mode)
#+END_SRC

** Modal Editing

Evil is an extensible vi layer for Emacs.

#+BEGIN_SRC emacs-lisp
  (use-package evil
    :ensure t
    :diminish undo-tree-mode
    :init
    (setq evil-want-keybinding nil
          evil-want-C-u-scroll t)
    :config
    (defun backward-kill-line (arg)
      (interactive "p")
      (kill-line (- 1 arg)))
    (evil-define-key 'insert 'global
      (kbd "C-u") 'backward-kill-line)

    ;; Add textobj for entire buffer.
    (evil-define-text-object evil-entire-entire-buffer (count &optional beg end type)
      "Select entire buffer"
      (evil-range (point-min) (point-max)))
    (define-key evil-inner-text-objects-map "e" 'evil-entire-entire-buffer)
    (define-key evil-outer-text-objects-map "e" 'evil-entire-entire-buffer)

    (setq evil-mode-line-format nil)
    (evil-mode 1))
#+END_SRC

Don't blink the cursor and use a separate cursor color in Emacs mode.

#+BEGIN_SRC emacs-lisp
  (blink-cursor-mode 0)
  (setq evil-normal-state-cursor '(box "#000000")
        evil-emacs-state-cursor  '(box "#7F5AB6"))
#+END_SRC

Use a blinking bar-style cursor in insert mode.

#+BEGIN_SRC emacs-lisp
  (defun ajgrf/evil-set-cursor-blink ()
    (blink-cursor-mode (if (evil-insert-state-p)
                           1
                         0)))

  (setq evil-insert-state-cursor  '(bar "#000000"))
  (add-hook 'evil-insert-state-entry-hook (lambda () (blink-cursor-mode 1)))
  (add-hook 'evil-insert-state-exit-hook  (lambda () (blink-cursor-mode 0)))
  (add-hook 'buffer-list-update-hook #'ajgrf/evil-set-cursor-blink)
#+END_SRC

*** Workman Layout

I need to use Workman bindings in evil-mode because I'm a snowflake.

#+BEGIN_SRC emacs-lisp
  (setq evil-workman (getenv "WORKMAN"))
#+END_SRC

Define the keys to translate.

#+BEGIN_SRC emacs-lisp
  (defvar workman-base-translations
    (list "n" "j"
          "e" "k"
          "y" "h"
          "o" "l"
          "j" "y"
          "k" "n"
          "h" "e"
          "l" "o")
    "The basic evil keys to translate for the Workman keyboard layout.")

  (defvar workman-translations
    (append workman-base-translations
            (mapcar #'upcase workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "C-" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "C-S-" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "M-" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "M-" (upcase c))))
                    workman-base-translations))
    "Evil keys to translate for the Workman keyboard layout.")

  (defvar workman-extended-translations
    (append workman-translations
            (mapcar (lambda (c) (kbd (concat "g" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "g" (upcase c))))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "z" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "z" (upcase c))))
                    workman-base-translations))
    "Extended set of Workman key translations (for evil keymaps).")
#+END_SRC

Fix my movement keys in modes that don't translate quite right.

#+BEGIN_SRC emacs-lisp
  (defmacro evil-add-yneo-bindings (keymap &optional state &rest bindings)
    "Add \"y\", \"n\", \"e\", \"o\" bindings to KEYMAP in STATE.
  Add additional BINDINGS if specified."
    (declare (indent defun))
    `(when evil-workman
       (evil-define-key ,state ,keymap
         "y" (lookup-key evil-motion-state-map "y")
         "n" (lookup-key evil-motion-state-map "n")
         "e" (lookup-key evil-motion-state-map "e")
         "o" (lookup-key evil-motion-state-map "o")
         ":" (lookup-key evil-motion-state-map ":")
         ,@bindings)))
#+END_SRC

Set up the translation in evil-collection's config.

#+NAME: evil-collection-workman
#+BEGIN_SRC emacs-lisp :tangle no
  (defun workman-translate-keys (mode keymaps &optional states &rest _rest)
    (let ((translations (if (or states (eq mode 'evil-mode))
                            workman-extended-translations
                          workman-translations)))
      (when (and evil-workman keymaps)
        (apply #'evil-collection-translate-key
               states
               keymaps
               translations))))

  (workman-translate-keys 'evil-mode
                          '(evil-normal-state-map
                            evil-motion-state-map
                            evil-visual-state-map
                            evil-window-map))

  (add-hook 'evil-collection-setup-hook #'workman-translate-keys)
#+END_SRC

*** Integration

Integrate evil with much of the rest of Emacs.

#+BEGIN_SRC emacs-lisp :noweb yes
  (use-package evil-collection
    :ensure t
    :after evil
    :config
    <<evil-collection-workman>>
    (evil-collection-init))
#+END_SRC

*** Surround

Edit pairs of surroundings together, like parentheses, brackets, quotes, tags.

#+BEGIN_SRC emacs-lisp
  (use-package evil-surround
    :ensure t
    :after evil
    :config
    (global-evil-surround-mode 1))
#+END_SRC

*** Matchit

Extend % to jump between matching tags or code branches.

#+BEGIN_SRC emacs-lisp
  (use-package evil-matchit
    :ensure t
    :after evil
    :config
    (global-evil-matchit-mode 1))
#+END_SRC

*** Commentary

Easily comment stuff out.

#+BEGIN_SRC emacs-lisp
  (use-package evil-commentary
    :ensure t
    :diminish
    :config
    (workman-translate-keys 'evil-commentary-mode
                            'evil-commentary-mode-map
                            'normal)
    (evil-commentary-mode))
#+END_SRC

** Keybinding Popup

Show a popup with completions for partially-entered keybindings.

#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :ensure t
    :diminish
    :config (which-key-mode 1))
#+END_SRC

** Leader Keys

Use general.el to manage keybindings more easily and set up
Spacemacs-like leader keys.

#+BEGIN_SRC emacs-lisp
  (use-package general
    :ensure t
    :config
    (general-override-mode 1)
    (general-auto-unbind-keys)

    (defun find-emacs-config ()
      "Edit my Emacs configuration file in the current window."
      (interactive)
      (find-file-existing "/vcsh:dotfiles:~/.emacs.d/emacs.org"))

    (defun reload-emacs-config ()
      "Reload my Emacs configuration."
      (interactive)
      (require 'org)
      (org-babel-tangle-file "/vcsh:dotfiles:~/.emacs.d/emacs.org")
      (load-file user-init-file))

    (defun set-font-size (size)
      (interactive "NSet Font Size: ")
      (set-face-attribute 'default
                          (selected-frame)
                          :height (* 10 size)))

    (defun text-scale-reset ()
      "Disable text-scale-mode, returning text to normal size."
      (interactive)
      (text-scale-mode 0))

    ;; Modified from Magnars of Emacs Rocks (https://emacsrocks.com/).
    ;; https://stackoverflow.com/a/37456354
    (defun rename-current-buffer-file ()
      "Renames current buffer and file it is visiting."
      (interactive)
      (let* ((name (buffer-name))
             (filename (buffer-file-name))
             (basename (file-name-nondirectory filename)))
        (if (not (and filename (file-exists-p filename)))
            (error "Buffer '%s' is not visiting a file!" name)
          (let ((new-name (read-file-name "New name: " (file-name-directory filename) basename nil basename)))
            (if (get-buffer new-name)
                (error "A buffer named '%s' already exists!" new-name)
              (rename-file filename new-name 1)
              (rename-buffer new-name)
              (set-visited-file-name new-name)
              (set-buffer-modified-p nil)
              (message "File '%s' successfully renamed to '%s'"
                       name (file-name-nondirectory new-name)))))))

    (general-create-definer tyrant-def
      :states '(normal visual insert motion emacs)
      :keymaps 'override
      :prefix "SPC"
      :non-normal-prefix "C-SPC")

    (general-define-key
      :states '(normal visual)
      "," (general-simulate-key "SPC m"))

    (general-define-key
      :states 'insert
      "C-," (general-simulate-key "C-SPC m"))

    (tyrant-def
     "a"   '(:ignore t :which-key "app")
     "ac"  'calc
     "ad"  'dired
     "ak"  'list-packages
     "aP"  'proced
     "as"  '(:ignore t :which-key "shell")
     "ast" 'ansi-term
     "au"  'undo-tree-visualize

     "b"   '(:ignore t :which-key "buffer")
     "bb"  'ivy-switch-buffer
     "bd"  'evil-delete-buffer
     "bD"  'kill-current-buffer
     "bl"  'evil-switch-to-windows-last-buffer
     "bR"  'rename-current-buffer-file
     "bw"  'read-only-mode

     "f"   '(:ignore t :which-key "file")
     "fb"  'bookmark-jump
     "ff"  'find-file
     "fe"  '(:ignore t :which-key "emacs")
     "fed" 'find-emacs-config
     "feR" 'reload-emacs-config
     "fl"  'find-library

     "h"   '(:ignore t :which-key "help")
     "ha"  'apropos-command
     "hb"  'describe-bindings
     "hc"  'describe-key-briefly
     "hf"  'describe-function
     "hF"  'describe-face
     "hh"  'help
     "hi"  'info
     "hk"  'describe-key
     "hm"  'describe-mode
     "hM"  'man
     "hP"  'describe-package
     "hv"  'describe-variable

     "m"   '(:ignore t :which-key "mode")

     "q"   '(:ignore t :which-key "quit")
     "qq"  'save-buffers-kill-terminal
     "qQ"  'save-buffers-kill-emacs

     "s"   '(:ignore t :which-key "search")

     "t"   '(:ignore t :which-key "toggles")
     "tF"  'auto-fill-mode
     "th"  '(:ignore t :which-key "highlight")
     "thh" 'global-hl-line-mode
     "thl" 'highlight-lines-matching-regexp
     "thr" 'highlight-regexp
     "thu" 'unhighlight-regexp
     "thU" 'hi-lock-mode
     "tl"  'toggle-truncate-lines
     "tn"  'display-line-numbers-mode
     "tw"  'whitespace-mode

     "T"   '(:ignore t :which-key "UI toggles/themes")
     "Tf"  'fringe-foo
     "TF"  'toggle-frame-fullscreen
     "TM"  'toggle-frame-maximized
     "Tm"  'menu-bar-mode
     "Ts"  'load-theme
     "Tt"  'tool-bar-mode

     "w"   '(evil-window-map :which-key "window")

     "z"   '(:ignore t :which-key "zoom")
     "zs"  'set-font-size
     "zz"  'text-scale-adjust
     "zi"  'text-scale-increase
     "zo"  'text-scale-decrease
     "z0"  'text-scale-reset))
#+END_SRC

Restart Emacs.

#+BEGIN_SRC emacs-lisp
  (use-package restart-emacs
    :ensure t
    :commands restart-emacs
    :general (tyrant-def "qR" 'reload-and-restart-emacs)
    :config
    (defun reload-and-restart-emacs ()
      "Reload Emacs configuration and restart Emacs."
      (interactive)
      (require 'org)
      (org-babel-tangle-file "/vcsh:dotfiles:~/.emacs.d/emacs.org")
      ;; (setq restart-emacs-restore-frames t)
      (restart-emacs)))
#+END_SRC

** Multiple Cursors

Edit text with multiple cursors.

#+BEGIN_SRC emacs-lisp
  (use-package evil-mc
    :diminish
    :general
    (general-define-key
     :states '(normal visual)
     "gsm" 'evil-mc-make-all-cursors
     "gsu" 'evil-mc-undo-last-added-cursor
     "gsq" 'evil-mc-undo-all-cursors
     "gss" 'evil-mc-pause-cursors
     "gsr" 'evil-mc-resume-cursors
     "gsf" 'evil-mc-make-and-goto-first-cursor
     "gsl" 'evil-mc-make-and-goto-last-cursor
     "gsh" 'evil-mc-make-cursor-here
     "M-p" 'evil-mc-make-and-goto-prev-cursor
     "gsP" 'evil-mc-skip-and-goto-prev-cursor
     "C-t" 'evil-mc-skip-and-goto-next-match
     "C-p" 'evil-mc-make-and-goto-prev-match
     "gsp" 'evil-mc-skip-and-goto-prev-match
     "C-x" 'evil-mc-skip-and-goto-next-match
     ;; workman vim bindings
     "gsn" 'evil-mc-make-cursor-move-next-line
     "gse" 'evil-mc-make-cursor-move-prev-line
     "M-k" 'evil-mc-make-and-goto-next-cursor
     "gsK" 'evil-mc-skip-and-goto-next-cursor
     "C-k" 'evil-mc-make-and-goto-next-match
     "gsk" 'evil-mc-skip-and-goto-next-match
     "C-n" 'evil-mc-make-cursor-move-next-line
     "C-e" 'evil-mc-make-cursor-move-prev-line)
    (general-define-key
     :states 'visual
     "gsi" 'evil-mc-make-cursor-in-visual-selection-beg
     "gsa" 'evil-mc-make-cursor-in-visual-selection-end)
    (general-define-key
     :states 'normal
     "<escape>" 'evil-mc-undo-all-cursors)
    (general-define-key
     "C-<down-mouse-1>" nil
     "C-<mouse-1>" 'evil-mc-toggle-cursor-on-click)
    :config
    (global-evil-mc-mode 1))
#+END_SRC

** Completion

*** Auto-Completion

#+BEGIN_SRC emacs-lisp
  (use-package company
    :ensure t
    :diminish
    :hook (after-init . global-company-mode)
    :config
    (setq company-global-modes '(not eshell-mode))
    ;; tab key indents and completes
    (setq tab-always-indent 'complete)
    (defun company-backward-kill-line (arg)
      (interactive "p")
      (company-abort)
      (kill-line (- 1 arg)))
    (defun company-delete-backward-word ()
      (interactive)
      (company-abort)
      (evil-delete-backward-word))
    ;; For some reason binding C-u doesn't work in the :general keyword.
    (general-define-key
     :keymaps 'company-active-map
     "C-u" 'company-backward-kill-line
     "C-w" 'company-delete-backward-word))
#+END_SRC

*** Incremental Completion

Use ivy for generic input completion.

#+BEGIN_SRC emacs-lisp
  (use-package ivy
    :ensure t
    :diminish
    :hook (after-init . ivy-mode)
    :general
    (general-define-key
     :keymaps 'ivy-minibuffer-map
     "C-u"     'backward-kill-line
     "C-w"     'evil-delete-backward-word
     "C-b"     'ivy-scroll-down-command
     "C-f"     'ivy-scroll-up-command
     "<prior>" 'ivy-previous-history-element
     "<next>"  'ivy-next-history-element
     "<S-return>" 'ivy-immediate-done)
    :config
    (setq ivy-use-virtual-buffers t
          ivy-count-format "(%d/%d) "
          ivy-magic-tilde nil
          ivy-initial-inputs-alist nil
          ivy-re-builders-alist '((t . ivy--regex-ignore-order))))

  (use-package counsel
    :diminish
    :after ivy
    :general
    (tyrant-def
      "so" 'swiper
      "sr" 'counsel-rg
      "ss" 'counsel-ag)
    :config (counsel-mode))
#+END_SRC

*** Snippets

#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :ensure t
    :diminish yas-minor-mode
    :hook ((prog-mode org-mode) . yas-minor-mode)
    :general
    (tyrant-def
      "i"   '(:ignore t :which-key "insert")
      "is"  'yas-insert-snippet
      "iS"  '(:ignore t :which-key "snippet")
      "iSv" 'yas-visit-snippet-file
      "iSn" 'yas-new-snippet)
    :config
    (add-to-list 'yas-snippet-dirs
                 "~/.guix-profile/share/emacs/yasnippet-snippets" t)
    (add-hook 'git-commit-mode-hook
            (lambda ()
              (when (derived-mode-p 'text-mode)
                (yas-activate-extra-mode 'text-mode+git-commit-mode))))
    (yas/initialize))

  (use-package yasnippet-snippets
    :after yasnippet)
#+END_SRC

** Smartparens

Insert and delete parentheses and other pairs more intelligently.

#+BEGIN_SRC emacs-lisp
  (use-package smartparens
    :ensure t
    :diminish
    :hook ((clojure-mode emacs-lisp-mode lisp-mode scheme-mode) . turn-on-smartparens-strict-mode)
    :general
    (general-define-key
     :keymaps '(clojure-mode-map
                emacs-lisp-mode-map
                lisp-mode-map
                scheme-mode-map)
     :states 'normal
     "M-J" 'sp-join-sexp
     "M-s" 'sp-splice-sexp
     "M-S" 'sp-split-sexp
     "("   'sp-backward-sexp
     ")"   'sp-next-sexp
     "[["  'sp-backward-sexp
     "]]"  'sp-next-sexp
     "{"   'evil-backward-section-begin
     "}"   'evil-forward-section-begin
     "<"   'sp-forward-barf-sexp
     ">"   'sp-forward-slurp-sexp)
    (general-define-key
     :keymaps '(clojure-mode-map
                emacs-lisp-mode-map
                lisp-mode-map
                scheme-mode-map)
     :states 'insert
     "M-t" 'ajgrf/sp-reverse-transpose-sexp)
    :config
    (require 'smartparens-config)

    (defun ajgrf/sp-reverse-transpose-sexp (&optional arg)
      "Transpose the previous two S-expressions."
      (interactive "p")
      (sp-transpose-sexp (- (max 1 arg)))
      (sp-forward-sexp))

    ;; Expand curly brace blocks.
    (sp-with-modes '(awk-mode c-mode c++-mode css-mode go-mode java-mode js-mode
                     mhtml-mode nix-mode perl-mode rust-mode sh-mode)
      (sp-local-pair "{" nil :post-handlers '(:add ("||\n[i]" "RET"))))

    ;; Disable <> and {} pairs in web-mode.
    (eval-after-load 'smartparens-html
      '(sp-local-pair 'web-mode "<" nil :actions nil))

    (smartparens-global-mode))
#+END_SRC

Integrate Smartparens with Evil.

#+BEGIN_SRC emacs-lisp
  (use-package evil-smartparens
    :ensure t
    :diminish
    :hook (smartparens-enabled . evil-smartparens-mode)
    :config
    (defun ajgrf/fix-evil-sp-bindings ()
      (workman-translate-keys nil 'evil-smartparens-mode-map '(normal visual)))
    (advice-add 'evil-sp--add-bindings :after #'ajgrf/fix-evil-sp-bindings))
#+END_SRC

** Spell Check

Activate spell checker automatically in text mode, or manually with
keybindings.

#+BEGIN_SRC emacs-lisp
  (use-package flyspell
    :diminish
    :hook ((org-mode markdown-mode) . flyspell-mode)
    :general
    (tyrant-def
      "ts"  'flyspell-mode
      "tS"  'flyspell-prog-mode))
#+END_SRC

** Proportional Fonts

Use a mix of proportional fonts and fixed-width fonts where
appropriate. This applies to any mode based on text-mode, including
org and markdown.

#+BEGIN_SRC emacs-lisp
  (use-package mixed-pitch
    :diminish
    :general
    (tyrant-def "tm" 'mixed-pitch-mode)
    :commands mixed-pitch-mode
    :hook (org-mode . mixed-pitch-mode))
#+END_SRC

** Colors

Rainbow mode sets the background of color names to display their color.

#+BEGIN_SRC emacs-lisp
  (use-package rainbow-mode
    :diminish
    :general
    (tyrant-def
      "tC"  '(:ignore t :which-key "colors")
      "tCc" 'rainbow-mode))
#+END_SRC

* Window Management

Focus follows mouse.

#+BEGIN_SRC emacs-lisp
  (setq mouse-autoselect-window t)
#+END_SRC

Add a few window-related evil bindings.

#+BEGIN_SRC emacs-lisp
  (general-define-key
   :keymaps 'evil-window-map
   "d" 'evil-window-delete
   "F" 'make-frame)
#+END_SRC

** Winner Mode

Undo and redo window operations

#+BEGIN_SRC emacs-lisp
  (use-package winner
    :general
    (general-define-key
     :keymaps 'evil-window-map
     "u"   'winner-undo
     "C-r" 'winner-redo)
    :config
    (winner-mode 1))
#+END_SRC

** Workspaces

Multiple workspaces/tabs.

#+BEGIN_SRC emacs-lisp
  (use-package eyebrowse
    :general
    (general-define-key
      "M-1" 'eyebrowse-switch-to-window-config-1
      "M-2" 'eyebrowse-switch-to-window-config-2
      "M-3" 'eyebrowse-switch-to-window-config-3
      "M-4" 'eyebrowse-switch-to-window-config-4
      "M-5" 'eyebrowse-switch-to-window-config-5
      "M-6" 'eyebrowse-switch-to-window-config-6
      "M-7" 'eyebrowse-switch-to-window-config-7
      "M-8" 'eyebrowse-switch-to-window-config-8
      "M-9" 'eyebrowse-switch-to-window-config-9
      "M-0" 'eyebrowse-switch-to-window-config-0)
    (general-define-key
      :keymaps 'evil-motion-state-map
      "gt" 'eyebrowse-next-window-config
      "gT" 'eyebrowse-prev-window-config)
    (general-define-key
      :keymaps 'evil-window-map
      "x" 'eyebrowse-close-window-config
      "'" 'eyebrowse-last-window-config
      "1" 'eyebrowse-switch-to-window-config-1
      "2" 'eyebrowse-switch-to-window-config-2
      "3" 'eyebrowse-switch-to-window-config-3
      "4" 'eyebrowse-switch-to-window-config-4
      "5" 'eyebrowse-switch-to-window-config-5
      "6" 'eyebrowse-switch-to-window-config-6
      "7" 'eyebrowse-switch-to-window-config-7
      "8" 'eyebrowse-switch-to-window-config-8
      "9" 'eyebrowse-switch-to-window-config-9
      "0" 'eyebrowse-switch-to-window-config-0)
    :config
    (setq eyebrowse-mode-line-separator " "
          eyebrowse-mode-line-right-delimiter "] "
          eyebrowse-new-workspace t
          eyebrowse-wrap-around t)
    (eyebrowse-mode t))
#+END_SRC

** Dynamic Window Management

Edwina is my =dwm=-like dynamic window manager for Emacs.

#+BEGIN_SRC emacs-lisp
  (use-package edwina
    :load-path "~/src/edwina"
    :config
    (setq display-buffer-base-action '(display-buffer-below-selected)
          edwina-mode-line-format "")
    (edwina-setup-dwm-keys)
    (workman-translate-keys nil 'edwina-mode-map)
    (edwina-mode 1))
#+END_SRC

* Org Mode

#+BEGIN_SRC emacs-lisp
  (use-package org
    :hook (org-mode . org-indent-mode)
    :general
    (tyrant-def
      "o"   '(:ignore t :which-key "org")
      "oa"  'org-agenda-default
      "oc"  'org-capture
      "ol"  'org-store-link
      "oo"  'org-agenda
      "oO"  'org-clock-out
      "oq"  'org-clock-cancel
      "fp"  'find-plan-file)
    (tyrant-def org-mode-map
      "m,"  'org-ctrl-c-ctrl-c
      "m'"  'org-edit-special
      "m:"  'org-set-tags-command
      "m."  'org-time-stamp
      "ma"  'org-attach
      "mA"  'org-archive-subtree
      "mB"  '(:keymap org-babel-map :which-key "babel")
      "mC"  'org-clone-subtree-with-time-shift
      "md"  'org-deadline
      "me"  'org-export-dispatch
      "mI"  'org-clock-in
      "ml"  'org-insert-link
      "mn"  'org-toggle-narrow-to-subtree
      "mO"  'org-clock-out
      "mp"  'org-set-property
      "mq"  'org-clock-cancel
      "mR"  'org-refile
      "ms"  'org-schedule
      "mt"  'org-todo
      "mT"  '(:ignore t :which-key "toggles")
      "mTi" 'org-toggle-inline-images
      "mTl" 'org-toggle-link-display
      "mTo" 'org-toggle-ordered-property)
    (tyrant-def
      :definer 'minor-mode
      :keymaps 'org-src-mode
      "m," 'org-edit-src-exit
      "mc" 'org-edit-src-exit
      "mk" 'org-edit-src-abort
      "ma" 'org-edit-src-abort)
    :config
    (defun org-agenda-default ()
      (interactive)
      (org-agenda nil "n"))
    (defun my/get-org-files ()
      (directory-files org-directory t "\.org$"))
    (defun find-plan-file ()
      (interactive)
      (find-file-existing "~/org/plan.org"))
    (add-to-list 'org-modules 'org-attach)
    (add-to-list 'org-modules 'org-depend)
    (add-to-list 'org-modules 'org-habit)
    (setq holiday-bahai-holidays nil
          holiday-hebrew-holidays nil
          holiday-islamic-holidays nil
          holiday-oriental-holidays nil
          holiday-other-holidays '((holiday-fixed 5 5 "Cinco de Mayo")))
    (setq org-agenda-files '("~/org/plan.org" "~/org/training.org")
          org-agenda-span 'day
          org-agenda-timegrid-use-ampm t
          org-agenda-todo-ignore-scheduled t
          org-capture-templates
          '(("t" "Task" entry (file+headline "~/org/plan.org" "Tasks")
             "* TODO %?\n %i\n  %a\n")
            ("a" "Appointment" entry (file+headline "~/org/plan.org" "Calendar")
             "* %?\n %i\n  %a\n")
            ("f" "FOCUS Task" entry (file+headline "~/org/plan.org" "FOCUS")
             "* TODO %?\n %i\n  %a\n"))
          org-default-notes-file "~/org/inbox.org"
          org-image-actual-width nil
          org-link-abbrev-alist '(("attach" . org-attach-expand-link))
          org-outline-path-complete-in-steps nil
          org-refile-allow-creating-parent-nodes 'confirm
          org-refile-targets '((my/get-org-files :maxlevel . 3))
          org-refile-use-outline-path 'file
          org-return-follows-link t
          org-startup-folded 'showall
          org-startup-with-inline-images t
          org-todo-keywords '((sequence "TODO(t)" "WAITING(w)" "DONE(d!)"))))

  (use-package org-indent
    :diminish
    :commands org-indent-mode)

  (use-package org-bullets
    :commands org-bullets-mode
    :hook (org-mode . org-bullets-mode))

  (use-package evil-org
    :ensure t
    :diminish
    :after (evil evil-collection org)
    :hook (org-mode . evil-org-mode)
    :config
    (evil-org-set-key-theme)
    (evil-define-key 'normal outline-mode-map
      (kbd "TAB") 'org-cycle
      "["  nil
      "]"  nil
      "]]" 'outline-next-visible-heading
      "[[" 'outline-previous-visible-heading
      "^"  'evil-first-non-blank)
    (evil-define-key '(normal visual) evil-org-mode-map
      (kbd "RET")       'evil-org-return
      (kbd "<backtab>") 'org-shifttab)
    (workman-translate-keys 'org-mode
                            'evil-org-mode-map
                            '(normal motion visual))
    (require 'evil-org-agenda)
    (evil-org-agenda-set-keys)
    (workman-translate-keys 'org-mode 'org-agenda-mode-map))
#+END_SRC

** Reminders

Set up desktop notifications for org agenda items.

#+BEGIN_SRC emacs-lisp
  (use-package appt
    :defer 5
    :config
    (defun alert-appt-display (minutes-until _time msg)
      (require 'notifications)
      (notifications-notify
       :app-icon (concat user-emacs-directory "Org-mode-unicorn.svg")
       :title (concat minutes-until " Minute Reminder")
       :body msg))

    (setq appt-time-msg-list nil)  ;; clear existing appt list
    (setq appt-display-format 'window
          appt-display-interval 30
          appt-display-mode-line nil
          appt-disp-window-function #'alert-appt-display
          appt-message-warning-time 60)
    (appt-activate 1)
    (org-agenda-to-appt)
    (run-at-time "24:01" 3600 'org-agenda-to-appt)
    (add-hook 'org-agenda-finalize-hook 'org-agenda-to-appt))
#+END_SRC

https://joonro.github.io/blog/posts/toast-notifications-org-mode-windows.html

* Tools

** Inferior Processes

Comint mode runs interpreters in a buffer, adding common functionality
for line editing, history, keybindings, etc.

#+BEGIN_SRC emacs-lisp
  (use-package comint
    :general
    (tyrant-def comint-mode-map
      "m," 'comint-get-next-from-history
      "m." 'comint-insert-previous-argument
      "ml" 'comint-dynamic-list-input-ring)
    (general-define-key
     :keymaps 'comint-mode-map
     :states 'insert
     "SPC"       'comint-magic-space
     "C-a"       'move-beginning-of-line
     "C-e"       'move-end-of-line
     "C-k"       'kill-line
     "<prior>"   'comint-previous-matching-input-from-input
     "<next>"    'comint-next-matching-input-from-input
     "S-<prior>" 'scroll-down-command
     "S-<next>"  'scroll-up-command)
    :config
    (defun my-kill-buffer-sentinel (process signal)
      "Sentinel to kill buffer when its process exits."
      (and (memq (process-status process) '(exit signal))
           (buffer-live-p (process-buffer process))
           (evil-delete-buffer (process-buffer process))))

    (setq comint-completion-addsuffix '("/" . " ")))
#+END_SRC

*** Shell

Set up inferior shell, for running a shell in an Emacs buffer.

#+BEGIN_SRC emacs-lisp :noweb yes
  (use-package shell
    :general
    (tyrant-def
      "asi" 'shell)
    (general-define-key
     :keymaps 'shell-mode-map
     :states 'insert
     "TAB" 'company-complete
     "C-w" 'backward-delete-word)
    :config

    ;; Use Emacs as editor.
    (when (not (daemonp))
      (add-hook 'shell-mode-hook 'with-editor-export-editor))

    ;; Recognize the password prompt from my doas alias.
    (setq comint-password-prompt-regexp
          (concat comint-password-prompt-regexp
                  "\\|^doas (.*@.*) password: \\'"))

    ;;; Make C-w behave like bash:

    ;; https://www.emacswiki.org/emacs/BackwardDeleteWord
    (defun delete-word (arg)
      "Delete characters forward until encountering the end of a word.
  With argument, do this that many times."
      (interactive "p")
      (if (use-region-p)
          (delete-region (region-beginning) (region-end))
        (delete-region (point) (progn (forward-word arg) (point)))))

    (defun backward-delete-word (arg)
      "Delete characters backward until encountering the end of a word.
  With argument, do this that many times."
      (interactive "p")
      (delete-word (- arg)))

    ;; Redefine a few word characters.
    (add-hook 'shell-mode-hook
              (lambda ()
                (dolist (c '(?_ ?- ?.))
                  (modify-syntax-entry c "w"))
                (modify-syntax-entry ?/ "-")))

    ;;; Kill buffer when the shell exits.
    ;; https://emacs.stackexchange.com/a/48307

    (defun add-process-sentinel (sentinel &optional process)
      "Add SENTINEL to PROCESS.
  PROCESS defaults to the process of the current buffer.
  Use this function with care.
  If there is already a process sentinel SENTINEL is used as after-advice.
  That can fail if the process sentinel is reset by some other function."
      (unless process
        (setq process (get-buffer-process (current-buffer))))
      (let ((old (process-sentinel process)))
        (cond
         ((symbolp old)
          (advice-add old :after sentinel))
         ((null old)
          (set-process-sentinel process sentinel))
         (t (warn "Cannot set sentinel %S for process %S." sentinel process)))))

    (defun kill-shell-buffer-on-exit ()
      "Custom `shell-mode' behaviours."
      ;; Kill the buffer when the shell process exits.
      (add-process-sentinel 'my-kill-buffer-sentinel))

    (add-hook 'shell-mode-hook 'kill-shell-buffer-on-exit)

    <<apt-progress-bars>>)
#+END_SRC

**** Bash Completion

Enable Bash completion in ~shell-mode~ and shell scripts.

#+BEGIN_SRC emacs-lisp
  (use-package bash-completion
    :ensure t
    :commands (bash-completion-dynamic-complete
               bash-completion-dynamic-complete-nocomint)
    :init (add-hook 'shell-dynamic-complete-functions
                    'bash-completion-dynamic-complete))
#+END_SRC

**** Apt Progress Bars

Show =apt= progress bars in the minibuffer.

#+NAME: apt-progress-bars
#+BEGIN_SRC emacs-lisp :tangle no
  (advice-add 'ansi-color-apply-on-region :before 'ora-ansi-color-apply-on-region)

  (defun ora-ansi-color-apply-on-region (begin end)
    "Fix progress bars for e.g. apt(8).
  Display progress in the mode line instead."
    (let ((end-marker (copy-marker end))
          mb)
      (save-excursion
        (goto-char (copy-marker begin))
        (while (re-search-forward "\0337" end-marker t)
          (setq mb (match-beginning 0))
          (when (re-search-forward "\0338" end-marker t)
            (let ((progress (buffer-substring-no-properties
                             (+ mb 2) (- (point) 2))))
              (delete-region mb (point))
              (ora-apt-progress-message progress)))))))

  (defun ora-apt-progress-message (progress)
    (message
     (replace-regexp-in-string
      "%" "%%"
      (ansi-color-apply progress))))
#+END_SRC

https://oremacs.com/2019/03/24/shell-apt/

*** youtube-dl

#+BEGIN_SRC emacs-lisp
  (general-define-key
   :states 'normal
   "gy" 'ytdl-url)

  (defun run-command-in-buffer (name args)
    (let* ((new-buffer
            (apply 'make-comint-in-buffer name nil name nil args))
           (proc (get-buffer-process new-buffer)))
      (set-process-sentinel proc 'my-kill-buffer-sentinel)
      (switch-to-buffer-other-window new-buffer)))

  (defun ytdl-url (&optional url)
    "Run 'ytdl' over the URL.  If URL is nil, use URL at point."
    (interactive)
    (let ((url (or url (thing-at-point-url-at-point))))
      (run-command-in-buffer "ytdl" (list url))))
#+END_SRC

** Emacs Shell

A shell written entirely in elisp.

#+BEGIN_SRC emacs-lisp
  (use-package eshell
    :commands eshell
    :general
    (tyrant-def
      "ase" 'eshell)
    :config
    ;; For eshell's sudo
    (require 'em-tramp)

    ;; Open in new window
    (add-to-list 'display-buffer-alist
                 '("\\`\\*e?shell" display-buffer-below-selected))
    (setq eshell-banner-message ""
          eshell-destroy-buffer-when-process-dies t)
    (setq eshell-prompt-function
          (lambda ()
            (concat
             (when (not (= 0 eshell-last-command-status))
               (concat (number-to-string eshell-last-command-status) "|"))
             (abbreviate-file-name (eshell/pwd))
             (if (= (user-uid) 0) "# " "$ "))))
    (setq eshell-prompt-regexp "^[^#$\n]*[#$] ")

    (defun my-eshell-ctrl-d ()
      "Send EOF or exit Eshell, like Ctrl-D in terminal emulators."
      (interactive)
      ;; Send EOF if a process is running.
      (cond (eshell-process-list
             (eshell-send-eof-to-process))
            ;; Otherwise exit eshell if current input is empty.
            ((save-excursion
               (eshell-bol)
               (= (point) (point-at-eol)))
             (evil-delete-buffer (current-buffer)))))

    (defun eshell-toggle-scroll-to-bottom-on-output ()
      "Toggle `eshell-scroll-to-bottom-on-output'."
      (interactive)
      (setq eshell-scroll-to-bottom-on-output
            (not eshell-scroll-to-bottom-on-output)))

    (defun my-eshell-setup-keys ()
      (tyrant-def eshell-mode-map
        "mt"  '(:ignore t :which-key "toggles")
        "mts" 'eshell-toggle-scroll-to-bottom-on-output)
      (general-define-key
       :keymaps 'eshell-mode-map
       :states 'insert
       "<tab>"     'my-eshell-complete
       "C-a"       'eshell-bol
       "C-d"       'my-eshell-ctrl-d
       "C-e"       'move-end-of-line
       "C-k"       'kill-line
       "C-l"       'eshell/clear
       "C-u"       'eshell-kill-input
       "<up>"      'eshell-previous-input
       "<down>"    'eshell-next-input
       "<prior>"   'eshell-previous-matching-input-from-input
       "<next>"    'eshell-next-matching-input-from-input
       "S-<prior>" 'scroll-down-command
       "S-<next>"  'scroll-up-command))
    (add-hook 'eshell-first-time-mode-hook
              'my-eshell-setup-keys)

    (defun my-eshell-complete ()
      "Wrap pcomplete completions for the standard completion UI."
      (interactive)
      (pcomplete-std-complete))

    ;; Enable Bash completion in Eshell.
    (defun eshell-bash-completion ()
      (let ((bash-completion-nospace t))
        (while (pcomplete-here
                (nth 2 (bash-completion-dynamic-complete-nocomint
                        (save-excursion (eshell-bol) (point))
                        (point)))))))
    (when (require 'bash-completion nil t)
      (setq eshell-default-completion-function 'eshell-bash-completion)))
#+END_SRC

** Ledger

Ledger is a powerful, double-entry accounting system.

#+BEGIN_SRC emacs-lisp
  (use-package ledger-mode
    :ensure t
    :mode "\\.ledger\\'"
    :general
    (tyrant-def ledger-mode-map
      "mb"  'ledger-post-edit-amount
      "mc"  'ledger-toggle-current
      "md"  'ledger-delete-current-transaction
      "mf"  'ledger-occur
      "mi"  'ledger-add-transaction
      "ml"  'ledger-display-ledger-stats
      "mp"  'ledger-display-balance-at-point
      "mr"  'ledger-reconcile
      "mR"  'ledger-report
      "ms"  'ledger-sort-region
      "mt"  'ledger-insert-effective-date)
    (tyrant-def ledger-reconcile-mode-map
      "m," 'ledger-reconcile-toggle
      "ma" 'ledger-reconcile-quit
      "mk" 'ledger-reconcile-quit
      "mt" 'ledger-reconcile-change-target
      "m RET" 'ledger-reconcile-finish)
    (general-define-key
     :states  '(normal visual)
     :keymaps 'ledger-mode-map
     "gj"  'ledger-navigate-next-xact-or-directive
     "gk"  'ledger-navigate-prev-xact-or-directive
     "[["  'ledger-navigate-prev-xact-or-directive
     "]]"  'ledger-navigate-next-xact-or-directive
     "("   'ledger-navigate-beginning-of-xact
     ")"   'ledger-navigate-end-of-xact
     "="   (general-key-dispatch 'evil-indent
             "=" 'ledger-post-align-dwim))
    (general-define-key
     :states  'visual
     :keymaps 'ledger-mode-map
     "="   'evil-indent)
    (general-define-key
     :states  'normal
     :keymaps 'ledger-reconcile-mode-map
     "a"   'ledger-reconcile-add
     "c"   'ledger-reconcile-toggle
     "d"   'ledger-reconcile-delete
     "t"   'ledger-reconcile-change-target
     "gr"  'ledger-reconcile-refresh
     "q"   'ledger-reconcile-quit
     "ZQ"  'ledger-reconcile-quit
     "ZZ"  'ledger-reconcile-finish)
    (general-define-key
     :states  'normal
     :keymaps 'ledger-report-mode-map
     "q"   'ledger-report-quit)
    (workman-translate-keys 'ledger-mode
                            'ledger-mode-map
                            'normal)
    :config
    (setq ledger-init-file-name ".ledgerrc"
          ledger-post-amount-alignment-column 52
          ledger-reconcile-buffer-line-format "%(date)s  %-30(payee)s %-25(account)s %10(amount)s\n"
          ledger-reconcile-buffer-account-max-chars 25
          ledger-reconcile-buffer-payee-max-chars 30)
    (dolist (report '("summary" "balancesheet" "incomestatement" "budget"
                      "reconciled" "reimbursements" "monthly"))
      (add-to-list 'ledger-reports
                   (list report
                         (concat "./run-report.sh "
                                 report
                                 " --force-color -f %(ledger-file)"))))

    ;; Don't reindent previous line when inserting newline
    (add-hook 'ledger-mode-hook (lambda () (setq-local electric-indent-inhibit t)))

    ;; Only reconcile with real transactions
    (defun ledger-use-real-transactions (&rest ignore)
      (write-region "--real\n" nil ledger-init-file-name))
    (defun ledger-use-all-transactions (&rest ignore)
      (when (file-exists-p ledger-init-file-name)
        (delete-file ledger-init-file-name)))
    (advice-add 'ledger-reconcile :before #'ledger-use-real-transactions)
    (advice-add 'ledger-reconcile-quit :after #'ledger-use-all-transactions)
    (advice-add 'ledger-reconcile-finish :after #'ledger-use-all-transactions))
#+END_SRC

Show on-the-fly errors such as unbalanced transactions.

#+BEGIN_SRC emacs-lisp
  (use-package flycheck-ledger
    :after flycheck)
#+END_SRC

** File Management

#+BEGIN_SRC emacs-lisp
  (setq delete-by-moving-to-trash t)
  (setq dired-dwim-target t)
  (setq dired-listing-switches "-alh")
  (setq dired-guess-shell-alist-user
        '(("\\.info\\.json$" "ytdl")
          ("\\.(avi|mkv|mp4|webm)$" "mpv -fs")
          ("\\.(flac|m4a|mp3|ogg|opus)$" "mpv")
          ("\\.jpg$" "feh --cycle-once -dFZD-10 *")))
  (setq image-dired-external-viewermage nil)
  (add-to-list 'directory-abbrev-alist
    '("^/egnyte" . "/davs:focusengineering.egnyte.com:/webdav/Shared"))
#+END_SRC

** Documentation

*** Emacs

Configure the built-in Info documentation reader.

#+BEGIN_SRC emacs-lisp
  (use-package info
    :config
    (evil-add-yneo-bindings Info-mode-map 'normal
      "k" 'evil-search-next
      "?" 'evil-search-backward))
#+END_SRC

Enhance the built-in Emacs help with much more contextual information

#+BEGIN_SRC emacs-lisp
  (use-package helpful
    :ensure t
    :general
    (tyrant-def
      "hf" 'helpful-callable
      "hv" 'helpful-variable
      "hk" 'helpful-key))
#+END_SRC

*** Dash

Use Dash API documentation sets for offline docs.

#+BEGIN_SRC emacs-lisp
  (use-package counsel-dash
    :ensure t
    :commands (counsel-dash counsel-dash-install-docset)
    :general
    (tyrant-def
      "hd" 'counsel-dash)
    :config
    (setq counsel-dash-common-docsets
          (mapcar #'file-name-base
                  (directory-files counsel-dash-docsets-path
                                   nil
                                   "\\.docset$"))))
#+END_SRC

** REST Client

restclient.el is an interactive tool for exploring HTTP REST endpoints.

#+BEGIN_SRC emacs-lisp
  (use-package restclient
    :defer t
    :general
    (tyrant-def restclient-mode-map
      "m," 'restclient-http-send-current
      "mC" 'restclient-copy-curl-command
      "mN" 'restclient-narrow-to-current
      "mw" 'widen)
    (general-define-key
     :keymaps 'restclient-mode-map
     :states '(normal visual)
     "[[" 'restclient-jump-prev
     "]]" 'restclient-jump-next))
#+END_SRC

Provide completion of HTTP methods and headers.

#+BEGIN_SRC emacs-lisp
  (use-package company-restclient
    :after restclient
    :config
    (add-to-list 'company-backends 'company-restclient))
#+END_SRC

** Feed Aggregator

#+BEGIN_SRC emacs-lisp
  (use-package elfeed-org
    :ensure t
    :commands elfeed-org)

  (use-package elfeed
    :ensure t
    :general (tyrant-def "af" 'elfeed)
    :config
    (elfeed-org)
    (setq elfeed-db-directory "~/.local/share/elfeed"
          elfeed-enclosure-default-dir "~/tmp/"
          elfeed-search-filter "@1-month-ago +unread "
          rmh-elfeed-org-files (list (concat org-directory "/links.org")))
    (add-hook 'elfeed-new-entry-hook
              (elfeed-make-tagger :feed-title "LWN\\.net"
                                  :entry-title '("Kernel prepatch"
                                                 "Security-updates"
                                                 "Weekly Edition")
                                  :remove 'unread))
    (add-hook 'elfeed-new-entry-hook
              (elfeed-make-tagger :feed-title "Slate Star Codex"
                                  :entry-title '("Link" "OT" "Thread"
                                                 "Highlights")
                                  :remove 'unread))
    (add-hook 'elfeed-new-entry-hook
              (elfeed-make-tagger :feed-title "Barbell Logic Channel"
                                  :entry-title "^#[0-9]"
                                  :remove 'unread)))
#+END_SRC

** PDF Tools

PDF Tools is a full-featured PDF viewer embedded within Emacs which
improves upon the built-in DocView.

#+BEGIN_SRC emacs-lisp :noweb yes
  (use-package pdf-tools
    :diminish pdf-view-midnight-minor-mode
    :magic ("%PDF" . pdf-view-mode)
    :hook ((pdf-view-mode . pdf-view-auto-slice-minor-mode)
           (pdf-view-mode . pdf-view-midnight-minor-mode))
    :general
    (general-define-key
     :keymaps 'pdf-view-mode-map
     :states 'normal
     "j"     'evil-collection-pdf-view-next-line-or-next-page
     "k"     'evil-collection-pdf-view-previous-line-or-previous-page
     "J"     'pdf-view-next-page
     "K"     'pdf-view-previous-page
     "<tab>" 'pdf-outline)
    (tyrant-def pdf-view-mode-map
      "mtm" 'pdf-view-midnight-minor-mode)
    :config
    (setq pdf-view-midnight-colors '("#000000" . "#ffffea"))
    (workman-translate-keys 'pdf-view-mode
                            'pdf-view-mode-map
                            'normal)
    <<pdf-restore-page>>
    (pdf-tools-install))
#+END_SRC

Automatically open PDFs to the last-viewed page.

#+NAME: pdf-restore-page
#+BEGIN_SRC emacs-lisp :tangle no
  ;; workaround for pdf-tools not reopening to last-viewed page of the pdf:
  ;; https://github.com/politza/pdf-tools/issues/18#issuecomment-269515117
  (defun brds/pdf-set-last-viewed-bookmark ()
    (interactive)
    (when (eq major-mode 'pdf-view-mode)
      (bookmark-set (brds/pdf-generate-bookmark-name))))

  (defun brds/pdf-jump-last-viewed-bookmark ()
    (bookmark-set "fake") ; this is new
    (when
        (brds/pdf-has-last-viewed-bookmark)
      (bookmark-jump (brds/pdf-generate-bookmark-name))))

  (defun brds/pdf-has-last-viewed-bookmark ()
    (assoc
     (brds/pdf-generate-bookmark-name) bookmark-alist))

  (defun brds/pdf-generate-bookmark-name ()
    (concat "PDF-LAST-VIEWED: " (buffer-file-name)))

  (defun brds/pdf-set-all-last-viewed-bookmarks ()
    (dolist (buf (buffer-list))
      (with-current-buffer buf
        (brds/pdf-set-last-viewed-bookmark))))

  (add-hook 'kill-buffer-hook 'brds/pdf-set-last-viewed-bookmark)
  (add-hook 'pdf-view-mode-hook 'brds/pdf-jump-last-viewed-bookmark)
  (unless noninteractive  ; as `save-place-mode' does
    (add-hook 'kill-emacs-hook #'brds/pdf-set-all-last-viewed-bookmarks))
#+END_SRC

** Epub Reader

#+BEGIN_SRC emacs-lisp
  (use-package nov
    :mode ("\\.epub\\'" . nov-mode)
    :general
    (general-define-key
     :keymaps 'nov-mode-map
     :states 'normal
     "J" 'nov-next-document
     "K" 'nov-previous-document)
    :config
    (setq nov-text-width 79)
    (workman-translate-keys 'nov-mode
                            'nov-mode-map
                            'normal))
#+END_SRC

** Password Manager

Emacs interface to [[https://www.passwordstore.org/][pass]].

#+BEGIN_SRC emacs-lisp
  (use-package pass
    :ensure t
    :general (tyrant-def "ap" 'pass))
#+END_SRC

Use pass to store secrets.

#+BEGIN_SRC emacs-lisp
  (use-package auth-source-pass
    :config
    (auth-source-pass-enable))
#+END_SRC

** Email Client

=mu4e= is an email client for Emacs based on the =mu= (maildir-utils)
search engine.

#+BEGIN_SRC emacs-lisp
  (use-package mu4e
    :commands (mu4e mu4e~headers-jump-to-maildir)
    :general
    (defun mu4e-inbox ()
      (interactive)
      (mu4e~headers-jump-to-maildir "/Inbox"))
    (tyrant-def "am" 'mu4e-inbox)
    :config
    (setq mu4e-maildir       "~/mail"
          mu4e-sent-folder   "/Sent Items"
          mu4e-drafts-folder "/Drafts"
          mu4e-trash-folder  "/Trash"
          mu4e-get-mail-command "mbsync -c ~/.config/isync/mbsyncrc -a"))
#+END_SRC

Support links to mu4e messages from Org.

#+BEGIN_SRC emacs-lisp
  (use-package org-mu4e
    :after mu4e
    :config
    (setq org-mu4e-link-query-in-headers-mode t))
#+END_SRC

Show email threads in a unified conversation view.

#+BEGIN_SRC emacs-lisp
  (use-package mu4e-conversation
    :after mu4e
    :init
    (defalias 'copy-seq 'cl-copy-seq)
    :config
    (global-mu4e-conversation-mode))
#+END_SRC

** Slack

#+BEGIN_SRC emacs-lisp
  (use-package slack
    :general
    (tyrant-def
      "aSs" 'slack-start
      "aSc" 'slack-channel-select
      "aSg" 'slack-group-select)
    :config
    (setq slack-buffer-emojify t
          slack-prefer-current-team t)
    (require 'password-store)
    (slack-register-team
     :name "intellectuallp"
     :default t
     :token (password-store-get "personal/slack.com/intellectuallp_token"))
    (slack-start))
#+END_SRC

** Media Player

EMMS is the Emacs Multimedia System. It plays multimedia files from
Emacs using a variety of external players.

#+BEGIN_SRC emacs-lisp
  (use-package emms
    :general
    (tyrant-def
      "aM"  '(:ignore t :which-key "music")
      "aMm" 'emms
      "aMM" 'emms
      "aMP" 'emms-pause
      "aM>" 'emms-next
      "aM<" 'emms-previous
      "aMs" 'emms-stop
      "aMa" 'emms-browse-by-artist
      "aMA" 'emms-browse-by-album
      "aMb" 'emms-browse-by-genre
      "aMg" 'emms-browse-by-genre
      "aMy" 'emms-browse-by-year
      "aMc" 'emms-browse-by-composer
      "aMp" 'emms-browse-by-performer)
    (tyrant-def emms-playlist-mode-map
      "ma" 'emms-browse-by-artist
      "mA" 'emms-browse-by-album
      "mb" 'emms-browse-by-genre
      "md" 'emms-add-directory-tree
      "mf" 'emms-add-file
      "mu" 'emms-add-url
      "mg" 'emms-browse-by-genre
      "my" 'emms-browse-by-year
      "mc" 'emms-browse-by-composer
      "mp" 'emms-browse-by-performer)
    :config
    (require 'emms)
    (require 'emms-info-libtag)
    (emms-all)
    (emms-history-load)
    (setq emms-info-functions '(emms-info-libtag)
          emms-mode-line-format "[%s]"
          emms-mode-line-mode-line-function 'emms-mode-line-playlist-current
          emms-player-list '(emms-player-mpv)
          emms-playing-time-style 'downtime
          emms-source-file-default-directory "~/music/")

    ;; Work around bug in older versions of evil-collection
    (general-define-key
     :keymaps 'emms-playlist-mode-map
     :states 'normal
     "C" 'emms-playlist-clear)

    ;; Work around unknown load order issue with evil-collection and
    ;; emms-with-inhibit-read-only-t

    (defun evil-collection-emms-playlist-mode-insert-newline-above ()
      "Insert a newline above point."
      (interactive)
      (emms-with-inhibit-read-only-t
       (evil-insert-newline-above)))

    (defun evil-collection-emms-playlist-mode-insert-newline-below ()
      "Insert a newline below point."
      (interactive)
      (emms-with-inhibit-read-only-t
       (evil-insert-newline-below)))

    (defun evil-collection-emms-playlist-mode-paste-before ()
      "Pastes the latest yanked playlist items before the cursor position.
  The return value is the yanked text."
      (interactive)
      (emms-with-inhibit-read-only-t
       (goto-char (point-at-bol))
       (yank)
       (emms-playlist-mode-correct-previous-yank)
       (evil-previous-line)
       (evil-beginning-of-line))))
#+END_SRC

Display the EMMS mode-line as a ticker to save space.

#+BEGIN_SRC emacs-lisp
  (use-package emms-mode-line-cycle
    :config
    (emms-mode-line-cycle 1)
    (setq emms-mode-line-cycle-max-width 24))
#+END_SRC

* Projects

The =projectile= package provides useful project-centric commands.

#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :ensure t
    :diminish
    :defer t
    :config
    (setq counsel-projectile-switch-project-action 'dired))
#+END_SRC

The =counsel-projectile= package enhances =projectile= with =ivy= completion.

#+BEGIN_SRC emacs-lisp
  (use-package counsel-projectile
    :ensure t
    :defer t
    :general
    (tyrant-def
      "p"  '(:keymap projectile-command-map
             :package counsel-projectile
             :which-key "projects"))
    :config
    (counsel-projectile-mode))
#+END_SRC

* Version Control

Magit is the best porcelain for git.

#+BEGIN_SRC emacs-lisp
  (use-package magit
    :ensure t
    :diminish auto-revert-mode
    :general
    (tyrant-def
      "g"  '(:ignore t :which-key "git")
      "gc" 'magit-clone
      "gf" 'magit-file-dispatch
      "gm" 'magit-dispatch
      "gs" 'magit-status))

  (use-package evil-magit
    :ensure t
    :after (evil evil-collection magit)
    :config
    (when evil-workman
      (evil-define-key '(normal visual) magit-mode-map
        "\C-n" 'magit-section-forward
        "gn"   'magit-section-forward-sibling
        "\C-e" 'magit-section-backward
        "ge"   'magit-section-backward-sibling
        "n"    'evil-next-visual-line
        "e"    'evil-previous-visual-line
        "j"    nil
        "jj"   'evil-yank-line
        "jr"   'magit-show-refs
        "js"   'magit-copy-section-value
        "jb"   'magit-copy-buffer-revision
        "y"    nil
        "/"    'evil-search-forward
        "k"    'evil-search-next
        "K"    'evil-search-previous)
      (evil-define-key 'visual magit-mode-map
        "j"    'evil-yank
        "y"    nil)
      (evil-define-key '(normal visual) magit-diff-mode-map
        "gn"   'magit-section-forward)
      (evil-define-key '(normal visual) 'magit-blob-mode-map
        "gn"   'magit-blob-next
        "ge"   'magit-blob-previous)
      (evil-define-key '(normal visual) 'git-commit-mode-map
        "gn"   'git-commit-next-message
        "ge"   'git-commit-prev-message)
      (evil-define-key 'normal 'magit-blame-read-only-mode-map
        "n"    'evil-next-visual-line
        "\C-n" 'magit-blame-next-chunk
        "gn"   'magit-blame-next-chunk
        "gN"   'magit-blame-next-chunk-same-commit
        "e"    'evil-previous-visual-line
        "\C-e" 'magit-blame-previous-chunk
        "ge"   'magit-blame-previous-chunk
        "gE"   'magit-blame-previous-chunk-same-commit)
      (evil-define-key 'normal git-rebase-mode-map
        "n"    'evil-next-visual-line
        "e"    'evil-previous-visual-line
        "\M-n" 'git-rebase-move-line-down
        "\M-e" 'git-rebase-move-line-up
        "h"    'git-rebase-edit)))
#+END_SRC

** Forge

Integrate issues and pull requests from GitHub and GitLab.

#+BEGIN_SRC emacs-lisp
  (use-package forge
    :after magit
    :config
    (setq forge-topic-list-limit -5))
#+END_SRC

** vcsh

Add TRAMP method to integrate Magit with vcsh.
https://github.com/magit/magit/issues/2939

#+BEGIN_SRC emacs-lisp
  (use-package tramp
    :defer t
    :general
    (tyrant-def
      "fd" 'find-dotfile)
    :config
    (add-to-list 'tramp-methods
                 '("vcsh"
                   (tramp-login-program "vcsh")
                   (tramp-login-args (("enter") ("%h")))
                   (tramp-remote-shell "/bin/sh")
                   (tramp-remote-shell-args ("-c"))))

    ;; Get TRAMP $PATH from "remote" profile
    (add-to-list 'tramp-remote-path 'tramp-own-remote-path)

    (defun find-dotfile ()
      (interactive)
      (let ((default-directory (concat "/vcsh:dotfiles:" (getenv "HOME") "/")))
        (call-interactively 'find-file)))

    ;; Don't open shells in vcsh
    (defun call-without-vcsh (fun &rest r)
      (let ((default-directory (replace-regexp-in-string "/vcsh:dotfiles:" ""
                                                         default-directory)))
        (apply fun r)))
    (advice-add 'shell :around 'call-without-vcsh))
#+END_SRC

* Programming

** Syntax Checking

Check syntax and other errors on the fly.

#+BEGIN_SRC emacs-lisp
  (use-package flycheck
    :ensure t
    :hook (after-init . global-flycheck-mode)
    :general
    (tyrant-def
      "e"  '(:ignore t :which-key "errors")
      "ec" 'flycheck-clear
      "ee" 'flycheck-explain-error-at-point
      "eh" 'flycheck-describe-checker
      "eL" 'goto-flycheck-error-list
      "el" 'flycheck-list-errors
      "en" 'flycheck-next-error
      "ep" 'flycheck-previous-error
      "eS" 'flycheck-set-checker-executable
      "es" 'flycheck-select-checker
      "ev" 'flycheck-verify-checker)
    (general-define-key
     :keymaps 'flycheck-mode-map
     :states '(normal visual)
     "[e"  'flycheck-previous-error
     "]e"  'flycheck-next-error
     "[l"  'flycheck-previous-error
     "]l"  'flycheck-next-error)
    :config
    (setq flycheck-mode-line-prefix "!")
    (add-hook 'org-src-mode-hook
              (lambda () (flycheck-mode 0))))
#+END_SRC

** direnv

Read direnv's =.envrc= files to load and unload environment variables depending
on the current directory.

#+BEGIN_SRC emacs-lisp
  (use-package direnv
    :config
    (setq direnv-always-show-summary nil)
    (direnv-mode))
#+END_SRC

* Languages

** Markdown

#+BEGIN_SRC emacs-lisp
  (use-package markdown-mode
    :ensure t
    :commands (markdown-mode gfm-mode)
    :mode (("README\\.md\\'" . gfm-mode)
           ("\\.md\\'" . markdown-mode)
           ("\\.mdwn\\'" . markdown-mode)
           ("\\.markdown\\'" . markdown-mode))
    :config (setq markdown-command "pandoc"))
#+END_SRC

** APL

#+BEGIN_SRC emacs-lisp
  (use-package gnu-apl-mode
    :disabled
    :commands gnu-apl
    :init
    (fset 'apl 'gnu-apl)
    :config
    (setq gnu-apl-show-keymap-on-startup nil
          gnu-apl-show-tips-on-start nil)
    (defun gnu-apl-input-hook ()
      (set-input-method "APL-Z"))
    (add-hook 'gnu-apl-interactive-mode-hook 'gnu-apl-input-hook)
    (add-hook 'gnu-apl-mode-hook 'gnu-apl-input-hook))

  ;; (set-fontset-font "fontset-default" '(#x2300 . #x23ff) "Iosevka Term Slab")
#+END_SRC

** C

Default to sane indent rules for C.

#+BEGIN_SRC emacs-lisp
  (setq c-default-style "linux")
  (add-hook 'c-mode-hook
            (lambda () (setq indent-tabs-mode t)))
#+END_SRC

** Go

Support for the Go programming language.

#+BEGIN_SRC emacs-lisp
  (use-package go-mode
    :ensure t
    :mode "\\.go\\'"
    :config
    (when (executable-find "goimports")
      (setq gofmt-command "goimports"))
    (add-hook 'before-save-hook 'gofmt-before-save)
    (add-hook 'go-mode-hook
              (lambda () (setq tab-width 4)))
    (when evil-workman
      (evil-define-key 'normal go-mode-map
        "E" 'godef-describe
        "K" 'evil-search-previous)))
#+END_SRC

Show function signatures and other information in the echo area when
hovering over things.

#+BEGIN_SRC emacs-lisp
  (use-package go-eldoc
    :hook (go-mode . go-eldoc-setup))
#+END_SRC

** Haskell

#+BEGIN_SRC emacs-lisp
  (use-package haskell-mode
    :ensure t
    :mode "\\.hs\\'")
#+END_SRC

** HTML/CSS

Emmet provides dynamic snippets for HTML and CSS files.

#+BEGIN_SRC emacs-lisp
  (use-package emmet-mode
    :ensure t
    :diminish
    :hook (sgml-mode web-mode css-mode)
    :config
    (setq emmet-indentation 2
          emmet-move-cursor-between-quotes t
          emmet-preview-default nil))
#+END_SRC


#+BEGIN_SRC emacs-lisp
  (use-package web-mode
    :ensure t
    :mode ("\\.ejs\\'")
    :config
    (setq web-mode-code-indent-offset 2
          web-mode-markup-indent-offset 2))
#+END_SRC

** JavaScript

#+BEGIN_SRC emacs-lisp :tangle no
  (use-package js2-mode
    :mode "\\.js\\'")
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package prettier-js
    :ensure t
    :diminish
    :hook (js-mode . prettier-js-mode)
    :config
    (setq js-indent-level 2
          prettier-js-show-errors 'echo))
#+END_SRC

** Lisp-like
*** Clojure

#+BEGIN_SRC emacs-lisp
  (use-package clojure-mode)

  (use-package cider
    :general
    (tyrant-def clojure-mode-map
      "msi" 'cider-jack-in)
    (tyrant-def cider-mode-map
      "mha" 'cider-apropos
      "meb" 'cider-load-buffer
      "msn" 'cider-repl-set-ns
      "msq" 'cider-quit)
    (general-define-key
     :keymaps 'cider-repl-mode-map
     :states 'insert
     "C-a"       'move-beginning-of-line
     "C-e"       'move-end-of-line
     "C-k"       'kill-line
     "<up>"      'cider-repl-backward-input
     "<down>"    'cider-repl-forward-input
     "<prior>"   'cider-repl-backward-input
     "<next>"    'cider-repl-forward-input
     "S-<prior>" 'scroll-down-command
     "S-<next>"  'scroll-up-command)
    :config
    (evil-add-yneo-bindings 'cider-mode-map 'normal
      "E" 'cider-doc)
    (add-hook 'cider-mode-hook 'eldoc-mode))
#+END_SRC

*** Scheme

Geiser runs a scheme interpreter to interact with alongside source buffers.

#+BEGIN_SRC emacs-lisp
  (use-package geiser
    :hook (scheme-mode . geiser-mode)
    :commands run-geiser
    :config
    (evil-add-yneo-bindings 'geiser-mode-map 'normal
      "E" 'geiser-doc-symbol-at-point
      "K" 'evil-search-previous)
    (setq geiser-active-implementations '(guile)
          geiser-default-implementation 'guile
          geiser-mode-start-repl-p nil)
    (with-eval-after-load 'geiser-guile
      (add-to-list 'geiser-guile-load-path "~/src/guix"))
    (with-eval-after-load 'yasnippet
      (add-to-list 'yas-snippet-dirs "~/src/guix/etc/snippets" t)))
#+END_SRC

Add some extra Guix-related functionality, both for interacting with
the package manager and hacking the scheme package definitions.

#+BEGIN_SRC emacs-lisp
  (use-package guix
    :general
    (tyrant-def
      "ag" 'guix-popup))
#+END_SRC

Open files with =.guile= file extension in =scheme-mode=.

#+BEGIN_SRC emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.guile\\'" . scheme-mode) t)
#+END_SRC

** sh

Configure shell script indentation style to match =shfmt=.

#+BEGIN_SRC emacs-lisp
  (use-package sh-script
    :mode ("\\.shinit\\'" . sh-mode)
    :init
    (setq sh-indent-after-continuation 'always
          sh-indent-for-case-alt '+
          sh-indent-for-case-label 0)
    (defvaralias 'sh-basic-offset 'tab-width)
    (add-hook 'sh-mode-hook
              (lambda ()
                (setq indent-tabs-mode t
                      tab-width 4)))
    :config
    (defun sh-bash-completion ()
      (interactive)
      (bash-completion-dynamic-complete-nocomint
       (save-excursion (sh-beginning-of-command) (point))
       (point)))
    (add-to-list 'sh-dynamic-complete-functions
                 'sh-bash-completion))
#+END_SRC

** Vimscript

#+BEGIN_SRC emacs-lisp
  (use-package vimrc-mode
    :mode "\\.vim\\(rc\\)?\\'")
#+END_SRC

** YAML

#+BEGIN_SRC emacs-lisp
  (use-package yaml-mode
    :mode "\\.yml\\'")
#+END_SRC
