#+TITLE: My Emacs Configuration
#+AUTHOR: Alex Griffin
#+STARTUP: content
#+PROPERTY: header-args :tangle ~/.emacs.d/init.el

* General

This file describes my GNU Emacs configuration in a literate
programming style. Run the Emacs command =org-babel-tangle= on this
document to generate the actual config.

** Initialization

*** Optimize startup time

From doom-emacs and John Wiegley.

#+BEGIN_SRC emacs-lisp
  ;;; -*- lexical-binding: t -*-

  (defvar file-name-handler-alist-old file-name-handler-alist)

  (setq file-name-handler-alist nil
        gc-cons-threshold 402653184
        gc-cons-percentage 0.6
        load-prefer-newer t
        package-enable-at-startup nil
        package--init-file-ensured t)

  (add-hook 'after-init-hook
            `(lambda ()
               (setq file-name-handler-alist file-name-handler-alist-old
                     gc-cons-threshold 800000
                     gc-cons-percentage 0.1)
               (garbage-collect))
            t)
#+END_SRC

*** Persistence Files

Use a separate file for customization system.

#+BEGIN_SRC emacs-lisp
  (setq custom-file (concat user-emacs-directory "custom.el"))
  (load custom-file t)
#+END_SRC

Don't litter the filesystem with backup or auto-save files.

#+BEGIN_SRC emacs-lisp
  (setq make-backup-files nil)
  (setq auto-save-file-name-transforms
        `((".*" ,(concat user-emacs-directory "auto-save/") t)))
#+END_SRC

*** Package Management

**** Specify Package Repositories

Use TLS for ELPA, and add MELPA as an additional repository.

#+BEGIN_SRC emacs-lisp
  (setq package-archives
        '(("gnu" . "https://elpa.gnu.org/packages/")
          ("melpa" . "https://melpa.org/packages/")
          ("melpa-stable" . "https://stable.melpa.org/packages/")))
#+END_SRC

**** Bootstrap use-package

Every other package will be managed by use-package.

#+BEGIN_SRC emacs-lisp
  (eval-when-compile
    (require 'package)
    (package-initialize)
    (unless (package-installed-p 'use-package)
      (package-refresh-contents)
      (package-install 'use-package))
    (require 'use-package)
    (setq use-package-always-ensure t)
    (use-package diminish))
  (require 'bind-key)
#+END_SRC

**** Automatic updates

Check for package updates every 7 days.

#+BEGIN_SRC emacs-lisp
  (use-package auto-package-update
    :config
    (setq auto-package-update-delete-old-versions t
          auto-package-update-hide-results t)
    (auto-package-update-maybe))
#+END_SRC

** Interface Elements

Emacs' interface is a little too busy for me by default.

Disable the tool bar and menu bar.

#+BEGIN_SRC emacs-lisp
  (if (fboundp 'menu-bar-mode) (menu-bar-mode -1))
  (if (fboundp 'tool-bar-mode) (tool-bar-mode -1))
#+END_SRC

Don't show the normal Emacs welcome screen or related messages.

#+BEGIN_SRC emacs-lisp
  (setq inhibit-startup-message t)
  (defun display-startup-echo-area-message ()
    (message ""))
#+END_SRC

Empty the scratch buffer and remove lisp-interaction-mode from it.

#+BEGIN_SRC emacs-lisp
  (setq initial-scratch-message nil
        initial-major-mode 'fundamental-mode)
#+END_SRC

Set the window title format.

#+BEGIN_SRC emacs-lisp
  (setq frame-title-format "%b - GNU Emacs")
  (setq icon-title-format frame-title-format)
#+END_SRC

Disambiguate buffer names by adding its file path to the name when needed.

#+BEGIN_SRC emacs-lisp
  (set uniquify-buffer-name-style 'forward)
#+END_SRC

Set default window geometry.

#+BEGIN_SRC emacs-lisp
  (if (display-graphic-p)
      (setq default-frame-alist
            '((width . 80)
              (height . 43))))
#+END_SRC

Scroll one line at a time.

#+BEGIN_SRC emacs-lisp
  (setq scroll-conservatively 10000)
#+END_SRC

Prompt for 'y' or 'n' instead of 'yes' or 'no'.

#+BEGIN_SRC emacs-lisp
  (fset 'yes-or-no-p 'y-or-n-p)
#+END_SRC

Save cursor position to resume editing files.

#+BEGIN_SRC emacs-lisp
  (setq save-place-file (concat user-emacs-directory "places"))
  (save-place-mode 1)
#+END_SRC

Disable that infernal beep!

#+BEGIN_SRC emacs-lisp
  (setq ring-bell-function 'ignore
        visible-bell nil)
#+END_SRC

Enable mouse support in terminal mode.

#+BEGIN_SRC emacs-lisp
  (xterm-mouse-mode 1)
#+END_SRC

Some miscellaneous settings from
[[https://github.com/technomancy/better-defaults][better-defaults]].

#+BEGIN_SRC emacs-lisp
  (setq save-interprogram-paste-before-kill t
        apropos-do-all t
        mouse-yank-at-point t
        require-final-newline t
        ediff-window-setup-function 'ediff-setup-windows-plain)
#+END_SRC

** Window Management

Focus follows mouse.

#+BEGIN_SRC emacs-lisp
  (setq mouse-autoselect-window t)
#+END_SRC

** Appearance

*** Theme

Use parchment, my own light theme inspired by Acme and Leuven.

#+BEGIN_SRC emacs-lisp
  (use-package parchment-theme
    :load-path "~/src/parchment"
    :config
    (setq custom-safe-themes t
          parchment-want-modify-tty-colors t)
    (load-theme 'parchment t))
#+END_SRC

Highlight matching braces & parentheses.

#+BEGIN_SRC emacs-lisp
  (show-paren-mode)
#+END_SRC

*** Fonts

#+BEGIN_SRC emacs-lisp
  (set-face-font 'default           "Go Mono 11")
  (set-face-font 'fixed-pitch       "Noto Mono 11")
  (set-face-font 'fixed-pitch-serif "Go Mono 11")
  (set-face-font 'variable-pitch    "Noto Sans 11")
#+END_SRC

**** Proportional Fonts

Use a mix of proportional fonts and fixed-width fonts where
appropriate. This applies to any mode based on text-mode, including
org and markdown.

#+BEGIN_SRC emacs-lisp
  (use-package mixed-pitch
    :diminish
    :commands mixed-pitch-mode
    :if window-system
    :hook (text-mode . mixed-pitch-mode))
#+END_SRC

*** Cursor

Highlight the line that the cursor is currently on.

#+BEGIN_SRC emacs-lisp
  (global-hl-line-mode)
#+END_SRC

Fix describe-face when using hl-line-mode. From
https://emacs.stackexchange.com/a/45719:

#+BEGIN_SRC emacs-lisp
  (defun my-face-at-point ()
    (symbol-name
     (or (let ((face (get-text-property (point) 'face)))
           (or (and (face-list-p face)
                    (car face))
               (and (symbolp face)
                    face)))
         'default)))

  (eval-after-load "hl-line"
    '(progn
       (advice-add 'counsel--face-at-point :override #'my-face-at-point)))
#+END_SRC

Don't blink the cursor and use a separate cursor color in Emacs mode.

#+BEGIN_SRC emacs-lisp
  (blink-cursor-mode 0)
  (setq evil-normal-state-cursor '(box "#000000")
        evil-emacs-state-cursor  '(box "#7F5AB6"))
#+END_SRC

Use a blinking bar-style cursor in insert mode.

#+BEGIN_SRC emacs-lisp
  (setq evil-insert-state-cursor  '(bar "#000000"))
  (add-hook 'evil-insert-state-entry-hook (lambda () (blink-cursor-mode 1)))
  (add-hook 'evil-insert-state-exit-hook  (lambda () (blink-cursor-mode 0)))
#+END_SRC

* Editing

** Whitespace

#+BEGIN_SRC emacs-lisp
  (setq whitespace-line-column 79
        whitespace-style '(face lines-tail trailing))
  (global-whitespace-mode 1)
  (diminish 'global-whitespace-mode)
#+END_SRC

Don't indent with tabs by default.

#+BEGIN_SRC emacs-lisp
  (setq-default indent-tabs-mode nil)
#+END_SRC

Load style settings from =.editorconfig=

#+BEGIN_SRC emacs-lisp
  (use-package editorconfig
    :diminish
    :hook (prog-mode . editorconfig-mode)
    :commands editorconfig-mode)
  #+END_SRC

Automatically trim whitespace only from lines edited.

#+BEGIN_SRC emacs-lisp
  (use-package ws-butler
    :diminish
    :hook (prog-mode . ws-butler-mode)
    :commands ws-butler-mode)
#+END_SRC

** Modal Editing

Evil is an extensible vi layer for Emacs.

#+BEGIN_SRC emacs-lisp
  (use-package evil
    :diminish undo-tree-mode
    :init
    (setq evil-want-keybinding nil
          evil-want-C-u-scroll t)
    :config
    (setq evil-mode-line-format nil)
    (evil-mode 1))
#+END_SRC

*** Workman Layout

I need to use Workman bindings in evil-mode because I'm a snowflake.

#+BEGIN_SRC emacs-lisp
  (setq evil-workman (getenv "WORKMAN"))
#+END_SRC

Define the keys to translate.

#+BEGIN_SRC emacs-lisp
  (defvar workman-base-translations
    (list "n" "j"
          "e" "k"
          "y" "h"
          "o" "l"
          "j" "y"
          "k" "n"
          "h" "e"
          "l" "o")
    "The basic evil keys to translate for the Workman keyboard layout.")

  (defvar workman-translations
    (append workman-base-translations
            (mapcar #'upcase workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "C-" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "M-" c)))
                    workman-base-translations))
    "Evil keys to translate for the Workman keyboard layout.")

  (defvar workman-extended-translations
    (append workman-translations
            (mapcar (lambda (c) (kbd (concat "g" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "g" (upcase c))))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "z" c)))
                    workman-base-translations)
            (mapcar (lambda (c) (kbd (concat "z" (upcase c))))
                    workman-base-translations))
    "Extended set of Workman key translations (for evil keymaps).")
#+END_SRC

Fix my movement keys in modes that don't translate quite right.

#+BEGIN_SRC emacs-lisp
  (defmacro evil-add-yneo-bindings (keymap &optional state &rest bindings)
    "Add \"y\", \"n\", \"e\", \"o\" bindings to KEYMAP in STATE.
  Add additional BINDINGS if specified."
    (declare (indent defun))
    `(when evil-workman
       (evil-define-key ,state ,keymap
         "y" (lookup-key evil-motion-state-map "y")
         "n" (lookup-key evil-motion-state-map "n")
         "e" (lookup-key evil-motion-state-map "e")
         "o" (lookup-key evil-motion-state-map "o")
         ":" (lookup-key evil-motion-state-map ":")
         ,@bindings)))
  (add-hook 'Info-mode-hook
            (lambda () (evil-add-yneo-bindings Info-mode-map 'normal)))
#+END_SRC

Set up the translation in evil-collection's config.

#+NAME: evil-collection-workman
#+BEGIN_SRC emacs-lisp :tangle no
  (defun workman-translate-keys (mode keymaps &optional states &rest _rest)
    (let ((translations (if (or states (eq mode 'evil-mode))
                            workman-extended-translations
                          workman-translations)))
      (when (and evil-workman keymaps)
        (apply #'evil-collection-translate-key
               states
               keymaps
               translations))))

  (workman-translate-keys 'evil-mode
                          '(evil-normal-state-map
                            evil-motion-state-map
                            evil-visual-state-map
                            evil-window-map))

  (add-hook 'evil-collection-setup-hook #'workman-translate-keys)
#+END_SRC

*** Integration

Integrate evil with much of the rest of Emacs.

#+BEGIN_SRC emacs-lisp :noweb yes
  (use-package evil-collection
    :after evil
    :config
    <<evil-collection-workman>>
    (evil-collection-init))
#+END_SRC

*** Surround

Edit pairs of surroundings together, like parentheses, brackets, quotes, tags.

#+BEGIN_SRC emacs-lisp
  (use-package evil-surround
    :after evil
    :config
    (global-evil-surround-mode 1))
#+END_SRC

*** Matchit

Extend % to jump between matching tags or code branches.

#+BEGIN_SRC emacs-lisp
  (use-package evil-matchit
    :after evil
    :config
    (global-evil-matchit-mode 1))
#+END_SRC

*** TODO Multiple Cursors

Edit text with multiple cursors. Disabled for now until I figure out bindings.

#+BEGIN_SRC emacs-lisp
  (use-package evil-mc
    :disabled
    :after evil
    :config
    (global-evil-mc-mode 1))
#+END_SRC

** Keybinding Popup

Show a popup with completions for partially-entered keybindings.

#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :pin melpa-stable
    :diminish
    :config (which-key-mode 1))
#+END_SRC

** Leader Keys

Use general.el to manage keybindings more easily and set up
Spacemacs-like leader keys.

#+BEGIN_SRC emacs-lisp
  (use-package general
    :config
    (general-override-mode 1)
    (general-auto-unbind-keys)

    (defun find-emacs-config ()
      "Edit my Emacs configuration file in the current window."
      (interactive)
      (find-file-existing "/vcsh:dotfiles:~/.emacs.d/emacs.org"))

    (defun reload-emacs-config ()
      "Reload my Emacs configuration."
      (interactive)
      (require 'org)
      (org-babel-tangle-file "/vcsh:dotfiles:~/.emacs.d/emacs.org")
      (load-file user-init-file))

    (general-create-definer tyrant-def
      :states '(normal visual insert motion emacs)
      :keymaps 'override
      :prefix "SPC"
      :non-normal-prefix "C-SPC")

    (general-define-key
      :states '(normal visual)
      "," (general-simulate-key "SPC m"))

    (tyrant-def
     "a"   '(:ignore t :which-key "app")
     "ac"  'calc
     "ad"  'dired
     "ak"  'list-packages
     "aP"  'proced
     "as"  '(:ignore t :which-key "shell")
     "asi" 'shell
     "ase" 'eshell
     "ast" 'ansi-term
     "au"  'undo-tree-visualize

     "b"   '(:ignore t :which-key "buffer")
     "bb"  'ivy-switch-buffer
     "bd"  'evil-delete-buffer
     "bl"  'evil-switch-to-windows-last-buffer
     "bw"  'read-only-mode

     "f"   '(:ignore t :which-key "file")
     "fb"  'bookmark-jump
     "ff"  'find-file
     "fe"  '(:ignore t :which-key "emacs")
     "fed" 'find-emacs-config
     "feR" 'reload-emacs-config

     "h"   '(:ignore t :which-key "help")
     "ha"  'apropos-command
     "hb"  'describe-bindings
     "hc"  'describe-key-briefly
     "hf"  'describe-function
     "hF"  'describe-face
     "hh"  'help
     "hi"  'info
     "hk"  'describe-key
     "hm"  'describe-mode
     "hM"  'man
     "hP"  'describe-package
     "hv"  'describe-variable

     "m"   '(:ignore t :which-key "mode")

     "q"   '(:ignore t :which-key "quit")
     "qq"  'save-buffers-kill-terminal

     "t"   '(:ignore t :which-key "toggles")
     "tF"  'auto-fill-mode
     "th"  '(:ignore t :which-key "highlight")
     "thh" 'global-hl-line-mode
     "thl" 'highlight-lines-matching-regexp
     "thr" 'highlight-regexp
     "thu" 'unhighlight-regexp
     "thU" 'hi-lock-mode
     "tl"  'toggle-truncate-lines
     "tm"  'mixed-pitch-mode
     "tn"  'display-line-numbers-mode
     "tw"  'whitespace-mode

     "T"   '(:ignore t :which-key "UI toggles/themes")
     "Tf"  'fringe-foo
     "TF"  'toggle-frame-fullscreen
     "TM"  'toggle-frame-maximized
     "Tm"  'menu-bar-mode
     "Ts"  'load-theme
     "Tt"  'tool-bar-mode

     "w"   '(evil-window-map :which-key "window"))

    (general-define-key
     :keymaps 'evil-window-map
     "d" 'evil-window-delete
     "F" 'make-frame))
#+END_SRC

Restart Emacs.

#+BEGIN_SRC emacs-lisp
  (use-package restart-emacs
    :commands restart-emacs
    :general (tyrant-def "qR" 'reload-and-restart-emacs)
    :config
    (defun reload-and-restart-emacs ()
      "Reload Emacs configuration and restart Emacs."
      (interactive)
      (reload-emacs-config)
      ;; (setq restart-emacs-restore-frames t)
      (restart-emacs)))
#+END_SRC

** Completion

*** Auto-Completion

#+BEGIN_SRC emacs-lisp
  (use-package company
    :diminish
    :hook (prog-mode . company-mode))
#+END_SRC

*** Incremental Completion

Use ivy for generic input completion.

#+BEGIN_SRC emacs-lisp
  (use-package ivy
    :diminish
    :hook (after-init . ivy-mode)
    :config
    (setq ivy-use-virtual-buffers t
          ivy-count-format "(%d/%d) "
          ivy-magic-tilde nil
          ivy-initial-inputs-alist nil
          ivy-re-builders-alist '((t . ivy--regex-ignore-order))))

  (use-package counsel
    :diminish
    :after ivy
    :config (counsel-mode))
#+END_SRC

*** Snippets

#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :diminish yas-minor-mode
    :hook ((prog-mode org-mode) . yas-minor-mode)
    :general
    (tyrant-def
      "i"   '(:ignore t :which-key "insert")
      "is"  'yas-insert-snippet
      "iS"  '(:ignore t :which-key "snippet")
      "iSv" 'yas-visit-snippet-file
      "iSn" 'yas-new-snippet))

  (use-package yasnippet-snippets
    :after yasnippet)
#+END_SRC

** Spell Check

Activate spell checker automatically in text mode, or manually with
keybindings.

#+BEGIN_SRC emacs-lisp
  (use-package flyspell
    :hook ((org-mode markdown-mode) . flyspell-mode)
    :general
    (tyrant-def
      "ts"  'flyspell-mode
      "tS"  'flyspell-prog-mode))
#+END_SRC

** Colors

Rainbow mode sets the background of color names to display their color.

#+BEGIN_SRC emacs-lisp
  (use-package rainbow-mode
    :general
    (tyrant-def
      "tC"  '(:ignore t :which-key "colors")
      "tCc" 'rainbow-mode))
#+END_SRC

* Org Mode

#+BEGIN_SRC emacs-lisp
  (use-package org
    :diminish org-indent-mode
    :hook (org-mode . org-indent-mode)
    :general
    (tyrant-def
      "o"   '(:ignore t :which-key "org")
      "oa"  'org-agenda-list
      "oc"  'org-capture
      "ol"  'org-store-link
      "oo"  'org-agenda)
    (tyrant-def org-mode-map
      "m," 'org-edit-special
      "mn" 'org-toggle-narrow-to-subtree)
    (tyrant-def
      :definer 'minor-mode
      :keymaps 'org-src-mode
      "m," 'org-edit-src-exit
      "mc" 'org-edit-src-exit
      "mk" 'org-edit-src-abort
      "ma" 'org-edit-src-abort)
    :config
    (setq org-agenda-files '("~/org/")
          org-default-notes-file (concat org-directory "/capture.org")
          org-outline-path-complete-in-steps nil
          org-refile-allow-creating-parent-nodes 'confirm
          org-refile-targets '((org-agenda-files :maxlevel . 3))
          org-refile-use-outline-path 'file
          org-startup-folded 'showall
          org-startup-with-inline-images t
          org-todo-keywords '((sequence "TODO" "WAITING" "NEXT" "DONE"))))

  (use-package org-bullets
    :commands org-bullets-mode
    :hook (org-mode . org-bullets-mode))

  (use-package evil-org
    :diminish
    :after (evil evil-collection org)
    :hook (org-mode . evil-org-mode)
    :config
    (evil-org-set-key-theme)
    (evil-define-key 'normal outline-mode-map
      (kbd "TAB") 'org-cycle
      "["  nil
      "]"  nil
      "]]" 'outline-next-visible-heading
      "[[" 'outline-previous-visible-heading
      "^"  'evil-first-non-blank)
    (evil-define-key '(normal visual) evil-org-mode-map
      (kbd "<backtab>") 'org-shifttab)
    (workman-translate-keys 'org-mode
                            'evil-org-mode-map
                            '(normal motion visual))
    (require 'evil-org-agenda)
    (evil-org-agenda-set-keys)
    (workman-translate-keys 'org-mode 'org-agenda-mode-map))
#+END_SRC

* Tools

** Shell

#+BEGIN_SRC emacs-lisp
  (setq comint-password-prompt-regexp
        (concat comint-password-prompt-regexp
                "\\|^doas (.*@.*) password: \\'"))
#+END_SRC

** Emacs Shell

TUI commands must be listed here so they can open in a terminal emulator.

#+BEGIN_SRC emacs-lisp :tangle ~/.emacs.d/eshell/login
  (add-to-list 'eshell-visual-commands "ncmpc") >/dev/null
#+END_SRC

Close terminal emulator when the command exits.

#+BEGIN_SRC emacs-lisp
  (setq eshell-destroy-buffer-when-process-dies t)
#+END_SRC

Add some aliases.

#+BEGIN_SRC emacs-lisp :tangle ~/.emacs.d/eshell/alias
  alias date *date $*
  alias edit find-file $1
  alias l ls $*
  alias mpc *mpc $*
#+END_SRC

** Ledger

#+BEGIN_SRC emacs-lisp
  (use-package ledger-mode
    :mode "\\.ledger\\'"
    :general
    (tyrant-def ledger-mode-map
      "mb"  'ledger-post-edit-amount
      "mc"  'ledger-toggle-current
      "md"  'ledger-delete-current-transaction
      "mf"  'ledger-occur
      "mi"  'ledger-add-transaction
      "ml"  'ledger-display-ledger-stats
      "mp"  'ledger-display-balance-at-point
      "mr"  'ledger-reconcile
      "mR"  'ledger-report
      "ms"  'ledger-sort-region
      "mt"  'ledger-insert-effective-date)
    (tyrant-def ledger-reconcile-mode-map
      "m," 'ledger-reconcile-toggle
      "ma" 'ledger-reconcile-quit
      "mk" 'ledger-reconcile-quit
      "mt" 'ledger-reconcile-change-target
      "m RET" 'ledger-reconcile-finish)
    (general-define-key
     :states  '(normal visual)
     :keymaps 'ledger-mode-map
     "gj"  'ledger-navigate-next-xact-or-directive
     "gk"  'ledger-navigate-prev-xact-or-directive
     "M-j" 'ledger-navigate-next-xact-or-directive
     "M-k" 'ledger-navigate-prev-xact-or-directive
     "["   'ledger-navigate-prev-xact-or-directive
     "]"   'ledger-navigate-next-xact-or-directive
     "("   'ledger-navigate-beginning-of-xact
     ")"   'ledger-navigate-end-of-xact
     "="   (general-key-dispatch 'evil-indent
             "=" 'ledger-post-align-dwim))
    (general-define-key
     :states  'visual
     :keymaps 'ledger-mode-map
     "="   'evil-indent)
    (general-define-key
     :states  'normal
     :keymaps 'ledger-reconcile-mode-map
     "a"   'ledger-reconcile-add
     "c"   'ledger-reconcile-toggle
     "d"   'ledger-reconcile-delete
     "t"   'ledger-reconcile-change-target
     "gr"  'ledger-reconcile-refresh
     "q"   'ledger-reconcile-quit
     "ZQ"  'ledger-reconcile-quit
     "ZZ"  'ledger-reconcile-finish)
    (general-define-key
     :states  'normal
     :keymaps 'ledger-report-mode-map
     "q"   'ledger-report-quit)
    (workman-translate-keys 'ledger-mode
                            'ledger-mode-map
                            'normal)
    :config
    (setq ledger-init-file-name ".ledgerrc"
          ledger-clear-whole-transactions t
          ledger-post-amount-alignment-column 52
          ledger-reconcile-buffer-line-format "%(date)s  %-30(payee)s %-25(account)s %10(amount)s\n"
          ledger-reconcile-buffer-account-max-chars 25
          ledger-reconcile-buffer-payee-max-chars 30)
    (dolist (report '("summary" "balancesheet" "incomestatement" "budget"
                      "reconciled" "reimbursements" "monthly"))
      (add-to-list 'ledger-reports
                   (list report
                         (concat "./run-report.sh "
                                 report
                                 " --force-color -f %(ledger-file)"))))

    ;; Only reconcile with real transactions
    (defun ledger-use-real-transactions (&rest ignore)
      (write-region "--real\n" nil ledger-init-file-name))
    (defun ledger-use-all-transactions (&rest ignore)
      (when (file-exists-p ledger-init-file-name)
        (delete-file ledger-init-file-name)))
    (advice-add 'ledger-reconcile :before #'ledger-use-real-transactions)
    (advice-add 'ledger-reconcile-quit :after #'ledger-use-all-transactions)
    (advice-add 'ledger-reconcile-finish :after #'ledger-use-all-transactions)

    (add-hook 'ledger-mode-hook
              (lambda () (mixed-pitch-mode 0)))
    (add-hook 'ledger-reconcile-mode-hook
              (lambda () (mixed-pitch-mode 0))))
#+END_SRC

** File Management

#+BEGIN_SRC emacs-lisp
  (setq dired-dwim-target t)
  (setq dired-guess-shell-alist-user
        '(("\\.info\\.json$" "ytdl")
          ("\\.pdf$" "zathura")
          ("\\.(avi|mkv|mp4|webm)$" "mpv -fs")
          ("\\.(flac|m4a|mp3|ogg|opus)$" "mpv")
          ("\\.jpg$" "feh --cycle-once -dFZD-10 *")))
  (setq image-dired-external-viewermage nil)
  (add-to-list 'directory-abbrev-alist
    '("^/egnyte" . "/davs:focusengineering.egnyte.com:/webdav/Shared"))
#+END_SRC

** Feed Aggregator

#+BEGIN_SRC emacs-lisp
  (use-package elfeed-org
    :commands elfeed-org)

  (use-package elfeed
    :general (tyrant-def "af" 'elfeed)
    :config
    (elfeed-org)
    (setq elfeed-db-directory "~/.local/share/elfeed"
          elfeed-enclosure-default-dir "~/tmp/"
          elfeed-search-filter "@1-month-ago +unread "
          rmh-elfeed-org-files (list (concat org-directory "/links.org")))
    (add-hook 'elfeed-new-entry-hook
              (elfeed-make-tagger :feed-title "LWN\\.net"
                                  :entry-title '("Kernel prepatch"
                                                 "Security-updates"
                                                 "Weekly Edition")
                                  :remove 'unread))
    (add-hook 'elfeed-new-entry-hook
              (elfeed-make-tagger :feed-title "Slate Star Codex"
                                  :entry-title '("Link" "OT" "Thread"
                                                 "Highlights")
                                  :remove 'unread))
    (add-hook 'elfeed-new-entry-hook
              (elfeed-make-tagger :feed-title "Barbell Logic Channel"
                                  :entry-title "^#[0-9]"
                                  :remove 'unread)))
#+END_SRC

** Epub Reader

#+BEGIN_SRC emacs-lisp
  (use-package nov
    :mode ("\\.epub\\'" . nov-mode))
#+END_SRC

** Password Manager

#+BEGIN_SRC emacs-lisp
  (use-package pass
    :general (tyrant-def "ap" 'pass))
#+END_SRC

* Version Control

Magit is the best porcelain for git.

#+BEGIN_SRC emacs-lisp
  (use-package magit
    :pin melpa-stable
    :diminish auto-revert-mode
    :general
    (tyrant-def
      "g"  '(:ignore t :which-key "git")
      "gc" 'magit-clone
      "gf" 'magit-file-popup
      "gm" 'magit-dispatch-popup
      "gs" 'magit-status))

  (use-package evil-magit
    :pin melpa-stable
    :after (evil evil-collection magit)
    :config
    (when evil-workman
      (evil-define-key '(normal visual) magit-mode-map
        "\C-n" 'magit-section-forward
        "gn"   'magit-section-forward-sibling
        "\C-e" 'magit-section-backward
        "ge"   'magit-section-backward-sibling
        "n"    'evil-next-visual-line
        "e"    'evil-previous-visual-line
        "j"    nil
        "jj"   'evil-yank-line
        "jr"   'magit-show-refs-popup
        "js"   'magit-copy-section-value
        "jb"   'magit-copy-buffer-revision
        "y"    nil
        "/"    'evil-search-forward
        "k"    'evil-search-next
        "K"    'evil-search-previous)
      (evil-define-key 'visual magit-mode-map
        "j"    'evil-yank
        "y"    nil)
      (evil-define-key '(normal visual) magit-diff-mode-map
        "gn"   'magit-section-forward)
      (evil-define-key '(normal visual) 'magit-blob-mode-map
        "gn"   'magit-blob-next
        "ge"   'magit-blob-previous)
      (evil-define-key '(normal visual) 'git-commit-mode-map
        "gn"   'git-commit-next-message
        "ge"   'git-commit-prev-message)
      (evil-define-key 'normal 'magit-blame-read-only-mode-map
        "n"    'evil-next-visual-line
        "\C-n" 'magit-blame-next-chunk
        "gn"   'magit-blame-next-chunk
        "gN"   'magit-blame-next-chunk-same-commit
        "e"    'evil-previous-visual-line
        "\C-e" 'magit-blame-previous-chunk
        "ge"   'magit-blame-previous-chunk
        "gE"   'magit-blame-previous-chunk-same-commit)
      (evil-define-key 'normal git-rebase-mode-map
        "n"    'evil-next-visual-line
        "e"    'evil-previous-visual-line
        "\M-n" 'git-rebase-move-line-down
        "\M-e" 'git-rebase-move-line-up
        "h"    'git-rebase-edit)))
#+END_SRC

Add TRAMP method to integrate Magit with vcsh.
https://github.com/magit/magit/issues/2939

#+BEGIN_SRC emacs-lisp
  (use-package tramp
    :defer t
    :config
    (add-to-list 'tramp-methods
                 '("vcsh"
                   (tramp-login-program "vcsh")
                   (tramp-login-args (("enter") ("%h")))
                   (tramp-remote-shell "/bin/sh")
                   (tramp-remote-shell-args ("-c")))))
#+END_SRC

* Languages

** Markdown

#+BEGIN_SRC emacs-lisp
  (use-package markdown-mode
    :commands (markdown-mode gfm-mode)
    :mode (("README\\.md\\'" . gfm-mode)
           ("\\.md\\'" . markdown-mode)
           ("\\.mdwn\\'" . markdown-mode)
           ("\\.markdown\\'" . markdown-mode))
    :config (setq markdown-command "pandoc"))
#+END_SRC

** APL

#+BEGIN_SRC emacs-lisp
  (use-package gnu-apl-mode
    :disabled
    :commands gnu-apl
    :init
    (fset 'apl 'gnu-apl)
    :config
    (setq gnu-apl-show-keymap-on-startup nil
          gnu-apl-show-tips-on-start nil)
    (defun gnu-apl-input-hook ()
      (set-input-method "APL-Z"))
    (add-hook 'gnu-apl-interactive-mode-hook 'gnu-apl-input-hook)
    (add-hook 'gnu-apl-mode-hook 'gnu-apl-input-hook))

  ;; (set-fontset-font "fontset-default" '(#x2300 . #x23ff) "Iosevka Term Slab")
#+END_SRC

** C
** Go

#+BEGIN_SRC emacs-lisp
  (use-package go-mode
    :mode "\\.go\\'"
    :config
    (setq gofmt-command "goimports")
    (add-hook 'before-save-hook 'gofmt-before-save)
    (when evil-workman
      (evil-define-key 'normal go-mode-map
        "E" 'godef-describe
        "K" 'evil-search-previous)))
#+END_SRC

** Haskell

#+BEGIN_SRC emacs-lisp
  (use-package haskell-mode
    :mode "\\.hs\\'")
#+END_SRC

** Lisp-like
*** Scheme

#+BEGIN_SRC emacs-lisp
  (use-package geiser
    :commands run-geiser
    :config
    (setq geiser-active-implementations '(guile)))

  (use-package guix-emacs
    :disabled
    :load-path "~/src/guix/emacs")
#+END_SRC

** sh
** Vimscript

#+BEGIN_SRC emacs-lisp
  (use-package vimrc-mode
    :mode "\\.vim\\(rc\\)?\\'")
#+END_SRC
